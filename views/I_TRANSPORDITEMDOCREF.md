---
name: I_TRANSPORDITEMDOCREF
description: Transporditemdocref
app_component: TM-FRM-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - TM
  - TM-FRM
  - interface-view
  - item-level
  - component:TM-FRM-2CL
  - lob:Other
---
# I_TRANSPORDITEMDOCREF

**Transporditemdocref**

| Property | Value |
|---|---|
| App Component | `TM-FRM-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `/scmtms/vdm_tor_itm_drf_uuid preserving type)` | `cast(db_key` |
| `/scmtms/vdm_tor_db_key preserving type)` | `cast(root_key` |
| `/scmtms/vdm_tor_item_db_key  preserving type)` | `cast(parent_key` |
| `TranspOrdItemDocReferenceID` | `btd_id` |
| `TranspOrdItemDocReferenceType` | `btd_tco` |
| `TranspOrdItmDocRefItemType` | `btditem_tco` |
| `TranspOrdItmDocReferenceItemID` | `btditem_id` |
| `TranspOrdItmDocRefDate` | `btd_date` |
| `TranspOrdItemDocRefIssuerName` | `btd_issuer` |
| `/* Associations */` | `/* Associations */` |
| `_TransportationOrder` | *Association* |
| `_TransportationOrderItem` | *Association* |
| `_BusTransDocTypeCode` | *Association* |
| `_BusTransDocItemTypeCode` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_TransportationOrder` | `I_TransportationOrder_2` | [1..1] |
| `_BusTransDocTypeCode` | `I_BusTransDocTypeCode` | [0..1] |
| `_BusTransDocItemTypeCode` | `I_BusTransDocItemTypeCode` | [0..1] |

## Source Code

```abap
@EndUserText.label: 'Transp Order Item Document Reference'
@ObjectModel:       { sapObjectNodeType.name:   'TransportationOrderItemDocRef',
                      representativeKey:        'TranspOrdItemDocReferenceUUID',
                      usageType:                { serviceQuality: #A,
                                                  sizeCategory: #XL,
                                                  dataClass: #TRANSACTIONAL },
                      supportedCapabilities: [ #SQL_DATA_SOURCE, #CDS_MODELING_DATA_SOURCE, #CDS_MODELING_ASSOCIATION_TARGET ],
                      modelingPattern: #ANALYTICAL_DIMENSION }
@AccessControl: {authorizationCheck:  #MANDATORY,
                 personalData.blocking:#('TRANSACTIONAL_DATA')}

@VDM:           {viewType:           #BASIC,
                 lifecycle.contract.type: #PUBLIC_LOCAL_API}
@Metadata.ignorePropagatedAnnotations: true                 

define view entity I_TranspOrdItemDocRef
  as select from /scmtms/d_toridr
  /* Business Object related Node Associations*/
  association        to parent I_TransportationOrderItem_2 as _TransportationOrderItem on $projection.TransportationOrderItemUUID = _TransportationOrderItem.TransportationOrderItemUUID
  association [1..1] to I_TransportationOrder_2            as _TransportationOrder     on $projection.TransportationOrderUUID = _TransportationOrder.TransportationOrderUUID
  association [0..1] to I_BusTransDocTypeCode              as _BusTransDocTypeCode     on $projection.TranspOrdItemDocReferenceType = _BusTransDocTypeCode.BusinessTransactionDocType
  association [0..1] to I_BusTransDocItemTypeCode          as _BusTransDocItemTypeCode on $projection.TranspOrdItmDocRefItemType = _BusTransDocItemTypeCode.BusinessTransacDocItmTypeCode

{
  key cast(db_key as /scmtms/vdm_tor_itm_drf_uuid preserving type)     as TranspOrdItemDocReferenceUUID,

      @ObjectModel.foreignKey.association: '_TransportationOrder'
      cast(root_key as /scmtms/vdm_tor_db_key preserving type)         as TransportationOrderUUID,

      @ObjectModel.foreignKey.association: '_TransportationOrderItem'
      cast(parent_key as /scmtms/vdm_tor_item_db_key  preserving type) as TransportationOrderItemUUID,

      btd_id                                                           as TranspOrdItemDocReferenceID,
      @ObjectModel.foreignKey.association: '_BusTransDocTypeCode'
      btd_tco                                                          as TranspOrdItemDocReferenceType,

      @ObjectModel.foreignKey.association: '_BusTransDocItemTypeCode'
      btditem_tco                                                      as TranspOrdItmDocRefItemType,
      btditem_id                                                       as TranspOrdItmDocReferenceItemID,
      btd_date                                                         as TranspOrdItmDocRefDate,
      btd_issuer                                                       as TranspOrdItemDocRefIssuerName,


      /* Associations */
      @ObjectModel.association.type:  [ #TO_COMPOSITION_ROOT ]
      _TransportationOrder,
      @ObjectModel.association.type:  [ #TO_COMPOSITION_PARENT ]
      _TransportationOrderItem,
      _BusTransDocTypeCode,
      _BusTransDocItemTypeCode
}
```
