---
name: I_PRODUCTTP_2
description: Producttp 2
app_component: LO-MD-MM-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - LO
  - LO-MD
  - LO-MD-MM
  - interface-view
  - transactional-processing
  - product
  - component:LO-MD-MM-2CL
  - lob:Logistics General
---
# I_PRODUCTTP_2

**Producttp 2**

| Property | Value |
|---|---|
| App Component | `LO-MD-MM-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `hidden: true }` | `hidden: true }` |
| `_ProductType_1 as _ProductType` | *Association* |
| `_CountryOfOrigin` | *Association* |
| `_ValuationClass` | *Association* |
| `_Brand` | *Association* |
| `_IndustrySector` | *Association* |
| `_ConsumptionValueCategoryText` | *Association* |
| `_ProductStatusText` | *Association* |
| `_IntArticleNumberText` | *Association* |
| `_ExtProdGrpText` | *Association* |
| `_SerialNoExplicitnessLevelTxt` | *Association* |
| `_ChemicalComplianceRlvtText` | *Association* |
| `_ArticleCategoryText` | *Association* |
| `_SalesUnitText` | *Association* |
| `_ProductMeasurementUnit` | *Association* |
| `_ContentUnit` | *Association* |
| `_TimeUnitForQuarantinePeriod` | *Association* |
| `_UnitForMaxPackaging` | *Association* |
| `_GoodsIssueUnit` | *Association* |
| `_ValuationClassText` | *Association* |

## Associations

> No associations found.

## Source Code

```abap
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Product - TP'

@VDM: {
  viewType: #TRANSACTIONAL,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
}
@ObjectModel: {
    usageType.serviceQuality: #A,
    usageType.sizeCategory : #L,
    usageType.dataClass: #MASTER,
    supportedCapabilities: [#TRANSACTIONAL_PROVIDER],
    modelingPattern: #TRANSACTIONAL_INTERFACE
}

@ObjectModel.representativeKey: 'Product'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.sapObjectNodeType.name: 'Product'


define root view entity I_ProductTP_2
  provider contract transactional_interface
  as projection on R_ProductTP as Product
{
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductStdVH', element : 'Product'} , useAsTemplate: true }]
  key Product,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductType', element : 'ProductType'} , useAsTemplate: true }]
      ProductType,
      CreationDate,
      CreationTime,
      CreationDateTime,
      CreatedByUser,
      LastChangeDate,
      LastChangedByUser,
      @Semantics.booleanIndicator: true
      IsMarkedForDeletion,
      @ObjectModel.text.association: '_ProductStatusText'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductStatus', element : 'Status'}, useAsTemplate: true }]
      CrossPlantStatus,
      CrossPlantStatusValidityDate,
      ProductOldID,
      @Semantics.quantity.unitOfMeasure: 'WeightUnit'
      GrossWeight,
      WeightUnit,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductGroup_2', element : 'ProductGroup'}, useAsTemplate: true }]
      ProductGroup,
      BaseUnit,
      ItemCategoryGroup,
      @Semantics.quantity.unitOfMeasure: 'WeightUnit'
      NetWeight,
      Division,
      VolumeUnit,
      @Semantics.quantity.unitOfMeasure: 'VolumeUnit'
      ProductVolume,
      AuthorizationGroup,
      ANPCode,
      SizeOrDimensionText,
      IndustryStandardName,
      ProductStandardID,
      @ObjectModel.text.association: '_IntArticleNumberText'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_Intntnlartnmbcat', element : 'InternationalArticleNumberCat'}, useAsTemplate: true }]
      InternationalArticleNumberCat,
      @Semantics.booleanIndicator: true
      ProductIsConfigurable,
      @Semantics.booleanIndicator: true
      IsBatchManagementRequired,
      @ObjectModel.text.association: '_ExtProdGrpText'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ExtProdGrp', element : 'ExternalProductGroup'}, useAsTemplate: true }]
      ExternalProductGroup,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductStdVH', element : 'Product'} , useAsTemplate: true }]
      CrossPlantConfigurableProduct,
      @ObjectModel.text.association: '_SerialNoExplicitnessLevelTxt'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_SerialNoExplicitnessLevel', element : 'SerialNoExplicitnessLevel'}, useAsTemplate: true }]
      SerialNoExplicitnessLevel,
      @Semantics.booleanIndicator: true
      IsApprovedBatchRecordReqd,
      HandlingIndicator,
      WarehouseProductGroup,
      WarehouseStorageCondition,
      StandardHandlingUnitType,
      SerialNumberProfile,
      @Semantics.booleanIndicator: true
      IsPilferable,
      @Semantics.booleanIndicator: true
      IsRelevantForHzdsSubstances,
      @Semantics.quantity.unitOfMeasure : 'TimeUnitForQuarantinePeriod'
      QuarantinePeriod,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UnitOfMeasureStdVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      TimeUnitForQuarantinePeriod,
      QualityInspectionGroup,
      HandlingUnitType,
      @Semantics.booleanIndicator: true
      HasVariableTareWeight,
      @Semantics.quantity.unitOfMeasure: 'UnitForMaxPackagingDimensions'
      MaximumPackagingLength,
      @Semantics.quantity.unitOfMeasure: 'UnitForMaxPackagingDimensions'
      MaximumPackagingWidth,
      @Semantics.quantity.unitOfMeasure: 'UnitForMaxPackagingDimensions'
      MaximumPackagingHeight,
      MaximumCapacity,
      OvercapacityTolerance,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UnitOfMeasureStdVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      UnitForMaxPackagingDimensions,
      @Semantics.quantity.unitOfMeasure: 'ProductMeasurementUnit'
      BaseUnitSpecificProductLength,
      @Semantics.quantity.unitOfMeasure: 'ProductMeasurementUnit'
      BaseUnitSpecificProductWidth,
      @Semantics.quantity.unitOfMeasure: 'ProductMeasurementUnit'
      BaseUnitSpecificProductHeight,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UnitOfMeasureStdVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      ProductMeasurementUnit,
      @ObjectModel.text.association: '_ArticleCategoryText'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductCategory', element : 'ProductCategory'}, useAsTemplate: true }]
      ArticleCategory,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_Industrysectorvh', element : 'IndustrySector'}, useAsTemplate: true }]
      IndustrySector,
      LastChangeDateTime,
      LastChangeTime,
      DangerousGoodsIndProfile,
      ProductDocumentChangeNumber,
      ProductDocumentPageCount,
      ProductDocumentPageNumber,
      @Semantics.booleanIndicator: true
      DocumentIsCreatedByCAD,
      ProductionOrInspectionMemoTxt,
      ProductionMemoPageFormat,
      @Semantics.booleanIndicator: true
      ProductIsHighlyViscous,
      @Semantics.booleanIndicator: true
      TransportIsInBulk,
      @Semantics.booleanIndicator: true
      ProdEffctyParamValsAreAssigned,
      @Semantics.booleanIndicator: true
      ProdIsEnvironmentallyRelevant,

      //@Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductLaboratoryOfficeVH', element : 'LaboratoryOrDesignOffice' } , useAsTemplate: true }]
      LaboratoryOrDesignOffice,
      PackagingProductGroup,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductStdVH', element : 'Product'} , useAsTemplate: true }]
      PackingReferenceProduct,
      BasicProduct,
      ProductDocumentNumber,
      ProductDocumentVersion,
      ProductDocumentType,
      ProductDocumentPageFormat,
//      @ObjectModel.text.association: '_ChemicalComplianceRlvtText'
//      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ChemicalComplianceRelevant', element : 'IsChemicalComplianceRelevant'} , useAsTemplate: true }]
      ProdChmlCmplncRelevanceCode,
      DiscountInKindEligibility,
      ProdCompetitorCustomerNumber,
      ProductHierarchy,
      ProdAllocDetnProcedure,
      // --- Retail
      @Feature: 'SW:CMD_API_PRODUCT'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_CountryVH', element : 'Country'} , useAsTemplate: true }]
      CountryOfOrigin,
      @Feature: 'SW:CMD_API_PRODUCT'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_Brand', element : 'Brand'}, useAsTemplate: true }]
      RetailArticleBrand,
      @Feature: 'SW:CMD_API_PRODUCT'
      ProductValidStartDate,
      // Not valid for Single Article
      //@Feature: 'SW:CMD_API_PRODUCT'
      //ProdNrOfGnrcProdInPrepckProds,
      @Feature: 'SW:CMD_API_PRODUCT'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UNITOFMEASURESTDVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      ContentUnit,
      @Feature: 'SW:CMD_API_PRODUCT'
      @Semantics.quantity.unitOfMeasure: 'ContentUnit'
      ProductNetContentQuantity,
      @Feature: 'SW:CMD_API_PRODUCT'
      @Semantics.quantity.unitOfMeasure: 'ContentUnit'
      ProductGrossContentQuantity,
      @Feature: 'SW:CMD_API_PRODUCT'
      ProductValidEndDate,
      @Feature: 'SW:CMD_API_PRODUCT'
      AssortmentListType,
      @Feature: 'SW:CMD_API_PRODUCT'
      TextilePartsIsWithAnimalOrigin,
      @Feature: 'SW:CMD_API_PRODUCT'
      ProductSeasonUsageCategory,
      @Feature: 'SW:CMD_API_PRODUCT'
      BillOfMaterialIsForEmpties,
      @Feature: 'SW:CMD_API_PRODUCT'
      ServiceAgreement,
      @Feature: 'SW:CMD_API_PRODUCT'
      @ObjectModel.text.association: '_ConsumptionValueCategoryText'
      @Consumption.valueHelpDefinition: [
       { entity:  { name:    'I_Prodabclassfctn',
         element: 'MaterialABCClassification' },
         useAsTemplate: true
       }]
      ConsumptionValueCategory,
      @Feature: 'SW:CMD_API_PRODUCT'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UNITOFMEASURESTDVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      GoodsIssueUnit,
      @Feature: 'SW:CMD_API_PRODUCT'
      RegionOfOrigin,
      @Feature: 'SW:CMD_API_PRODUCT'
      @ObjectModel.text.association: '_ValuationClassText'
      @Consumption.valueHelpDefinition: [
       { entity:  { name:    'I_Prodvaluationclass',
         element: 'ValuationClass' },
         useAsTemplate: true
       }]
      ValuationClass,
      // ValuationMarginPercent not valid for single article
      //@Feature: 'SW:CMD_API_PRODUCT'
      //ValuationMarginPercent,
      @Feature: 'SW:CMD_API_PRODUCT'
      @ObjectModel.text.association: '_SalesUnitText'
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_UNITOFMEASURESTDVH', element : 'UnitOfMeasure'} , useAsTemplate: true }]
      SalesUnit,
      ProductManufacturerNumber,
      ManufacturerNumber,
      ManufacturerPartProfile,
      @Consumption.valueHelpDefinition:[{ entity : { name: 'I_ProductStdVH', element : 'Product'} , useAsTemplate: true }]
      OwnInventoryManagedProduct,


      // ---
      /* Associations */
      _ProductDescription       : redirected to composition child I_ProductDescriptionTP_2,
      _ProductUnitOfMeasure     : redirected to composition child I_ProductUnitOfMeasureTP_2,
      _ProductPlant             : redirected to composition child I_ProductPlantTP_2,
      _ProductSalesDelivery     : redirected to composition child I_ProductSalesDeliveryTP_2,
      _ProductSales             : redirected to composition child I_ProductSalesTP_2,
      _ProductProcurement       : redirected to composition child I_ProductProcurementTP_2,
      _ProductQualityManagement : redirected to composition child I_ProductQualityManagementTP_2,
      _ProductValuation         : redirected to composition child I_ProductValuationTP_2,
      _ProductStorage           : redirected to composition child I_ProductStorageTP_2,
      _ProductEWMWarehouse      : redirected to composition child I_ProductEWMWarehouseTP_2,
      _ProductChangeMaster      : redirected to composition child I_ProductChangeMasterTP_2,
      _ProductBasicText         : redirected to composition child I_ProductBasicTextTP_2,
      _ProductInspectionText    : redirected to composition child I_ProductInspectionTextTP_2,
      _ProductPurchaseOrderText : redirected to composition child I_ProductPurchaseOrderTextTP_2,
      _ProductInternalComment   : redirected to composition child I_ProdInternalCommentTextTP_2,
      @Consumption: { hidden: true }
      _ProductGroup_2,
      @Consumption: { hidden: true }
      _ProductType_1 as _ProductType,

      _CountryOfOrigin,
      _ValuationClass,
      _Brand,
      _IndustrySector,
      _ConsumptionValueCategoryText,
      _ProductStatusText,
      _IntArticleNumberText,
      _ExtProdGrpText,
      _SerialNoExplicitnessLevelTxt,
      _ChemicalComplianceRlvtText,
      _ArticleCategoryText,
      _SalesUnitText,
      _ProductMeasurementUnit,
      _ContentUnit,
      _TimeUnitForQuarantinePeriod,
      _UnitForMaxPackaging,
      _GoodsIssueUnit,
      _ValuationClassText

}
```
