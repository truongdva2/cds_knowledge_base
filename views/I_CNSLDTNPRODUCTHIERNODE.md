---
name: I_CNSLDTNPRODUCTHIERNODE
description: Cnsldtnproducthiernode
app_component: FIN-CS-MD-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FIN
  - FIN-CS
  - FIN-CS-MD
  - interface-view
  - product
  - component:FIN-CS-MD-2CL
  - lob:Other
---
# I_CNSLDTNPRODUCTHIERNODE

**Cnsldtnproducthiernode**

| Property | Value |
|---|---|
| App Component | `FIN-CS-MD-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `name: 'I_CnsldtnProductVH'` | `name: 'I_CnsldtnProductVH'` |
| `element: 'Product'` | `element: 'Product'` |
| `}` | `}` |
| `}]` | `}]` |
| `Product` | `Product` |
| `_HierarchyNode.HierarchyNodeSequence` | *Association* |
| `_HierarchyNode.HierarchyNodeLevel` | *Association* |
| `_HierarchyNode.NodeType` | *Association* |
| `_Text` | *Association* |
| `_Product` | *Association* |
| `_Hierarchy` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_Product` | `I_CnsldtnProduct` | [0..1] |
| `_Hierarchy` | `I_CnsldtnProductHierarchy` | [1..1] |
| `_Text` | `I_CnsldtnProductHierNodeT` | [0..*] |

## Source Code

```abap
@Analytics: {
  dataExtraction.enabled: true
}
@Hierarchy.parentChild: [{
  recurse: {
    parent: ['ParentNode'],
    child:     ['HierarchyNode']
  },
  siblingsOrder: [{
    by: 'HierarchyNodeSequence',
    direction: #ASC
  }],
  directory: '_Hierarchy'
}]
@AccessControl:{
  authorizationCheck: #MANDATORY
}
@Metadata:{
  ignorePropagatedAnnotations: true
}
@ObjectModel: {
  usageType: {
    dataClass: #MASTER,
    serviceQuality: #B,
    sizeCategory: #M
  },
  dataCategory: #HIERARCHY,
  representativeKey: 'HierarchyNode',
  modelingPattern: #ANALYTICAL_PARENT_CHILD_HIERARCHY_NODE,
  supportedCapabilities: [#CDS_MODELING_DATA_SOURCE,
                          #CDS_MODELING_ASSOCIATION_TARGET,
                          #SQL_DATA_SOURCE,
                          #EXTRACTION_DATA_SOURCE,
                          #ANALYTICAL_PARENT_CHILD_HIERARCHY_NODE
                          ],
  sapObjectNodeType.name: 'CnsldtnProductHierarchyNode'
}
@VDM:{
  viewType: #BASIC,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
}
@EndUserText.label: 'Consolidation Product - Hierarchy Node'
/*+[hideWarning] { "IDS" : [ "KEY_CHECK" ]  } */
define view entity I_CnsldtnProductHierNode
  as select from P_CnsldtnUnivHierNode_3(P_HierarchyType : 'CS12', P_MasterDataType : 'MATNR_COPA' ) as _HierarchyNode

  association [0..1] to I_CnsldtnProduct          as _Product   on  $projection.Product = _Product.Product

  association [1..1] to I_CnsldtnProductHierarchy as _Hierarchy on  $projection.ConsolidationProductHierarchy = _Hierarchy.ConsolidationProductHierarchy
                                                                and $projection.ValidityEndDate               = _Hierarchy.ValidityEndDate

  association [0..*] to I_CnsldtnProductHierNodeT as _Text      on  $projection.ConsolidationProductHierarchy = _Text.ConsolidationProductHierarchy
                                                                and $projection.HierarchyNode                 = _Text.HierarchyNode
                                                                and $projection.Product                       = ''
{

      @Consumption.filter: {mandatory : true, selectionType : #SINGLE, multipleSelections : false }
      @ObjectModel.foreignKey.association: '_Hierarchy'
  key cast(_HierarchyNode.UniversalHierarchy as fincs_producthierarchy preserving type ) as ConsolidationProductHierarchy,

      @ObjectModel.text.association: '_Text'
  key _HierarchyNode.HierarchyNode,

      @Consumption.filter: {mandatory : true, selectionType : #SINGLE, multipleSelections : false }
      @Semantics.businessDate.to: true
  key _HierarchyNode.ValidityEndDate,

      @Semantics.businessDate.from: true
      _HierarchyNode.ValidityStartDate,

      _HierarchyNode.ParentNode,

      _HierarchyNode.HierarchyVersion,

      @ObjectModel.foreignKey.association: '_Product'
      @Consumption.valueHelpDefinition: [{
        entity: {
          name: 'I_CnsldtnProductVH',
          element: 'Product'
        }
      }]
      Product,

      _HierarchyNode.HierarchyNodeSequence,

      _HierarchyNode.HierarchyNodeLevel,

      _HierarchyNode.NodeType,


      _Text,
      _Product,
      _Hierarchy
};
```
