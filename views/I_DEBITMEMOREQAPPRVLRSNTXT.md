---
name: I_DEBITMEMOREQAPPRVLRSNTXT
description: Debitmemoreqapprvlrsntxt
app_component: SD-SLS-CMR-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - SD
  - SD-SLS
  - SD-SLS-CMR
  - interface-view
  - component:SD-SLS-CMR-2CL
  - lob:Sales & Distribution
---
# I_DEBITMEMOREQAPPRVLRSNTXT

**Debitmemoreqapprvlrsntxt**

| Property | Value |
|---|---|
| App Component | `SD-SLS-CMR-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `defaultSearchElement: true` | `defaultSearchElement: true` |
| `ranking: #HIGH` | `ranking: #HIGH` |
| `fuzzinessThreshold: 0.9` | `fuzzinessThreshold: 0.9` |
| `}` | `}` |
| `DebitMemoReqApprovalReason` | `DebitMemoReqApprvlRsnText.SalesDocApprovalReason` |
| `DebitMemoReqApprvlRsnName` | `DebitMemoReqApprvlRsnText.SalesDocApprovalReasonName` |
| `_DebitMemoReqApprvlRsnText` | *Association* |
| `DebitMemoReqApprvlRsnText._Language` | `DebitMemoReqApprvlRsnText._Language` |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_DebitMemoReqApprvlRsnText` | `I_DebitMemoReqApprovalReason` | [0..1] |

## Source Code

```abap
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@Analytics.dataExtraction.enabled: true
@EndUserText.label: 'Apprvl Reason for Debit Memo Req - Text'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.dataCategory: #TEXT
@ObjectModel.modelingPattern: #LANGUAGE_DEPENDENT_TEXT
@ObjectModel.usageType:{
  serviceQuality: #A,
  sizeCategory: #S,
  dataClass: #CUSTOMIZING
}
@ObjectModel.representativeKey: 'DebitMemoReqApprovalReason'
@ObjectModel.supportedCapabilities: [#CDS_MODELING_ASSOCIATION_TARGET,
                                     #CDS_MODELING_DATA_SOURCE,
                                     #EXTRACTION_DATA_SOURCE,
                                     #LANGUAGE_DEPENDENT_TEXT,
                                     #SEARCHABLE_ENTITY,
                                     #SQL_DATA_SOURCE]

@Search.searchable: true
@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API
@VDM.viewType: #BASIC
define view entity I_DebitMemoReqApprvlRsnTxt
  as select from I_SalesDocApprovalReasonText  as DebitMemoReqApprvlRsnText
    inner join   I_DebitMemoReqApprovalReason  as DebitMemoReqApprovalReason on DebitMemoReqApprovalReason.DebitMemoReqApprovalReason = DebitMemoReqApprvlRsnText.SalesDocApprovalReason

  association [0..1] to I_DebitMemoReqApprovalReason as _DebitMemoReqApprvlRsnText on $projection.DebitMemoReqApprovalReason = _DebitMemoReqApprvlRsnText.DebitMemoReqApprovalReason

{
      @Semantics.language: true
      @ObjectModel.foreignKey.association: '_Language'
  key DebitMemoReqApprvlRsnText.Language,

      @Search: {
        defaultSearchElement: true,
        ranking: #HIGH,
        fuzzinessThreshold: 0.9
      }
      @ObjectModel.foreignKey.association: '_DebitMemoReqApprvlRsnText'
  key DebitMemoReqApprvlRsnText.SalesDocApprovalReason     as DebitMemoReqApprovalReason,

      @Search.defaultSearchElement: true
      @Search.fuzzinessThreshold: 0.8
      @Search.ranking: #LOW
      @Semantics.text: true
      DebitMemoReqApprvlRsnText.SalesDocApprovalReasonName as DebitMemoReqApprvlRsnName,

      _DebitMemoReqApprvlRsnText,
      DebitMemoReqApprvlRsnText._Language

} where DebitMemoReqApprvlRsnText.SDDocumentCategory = 'L';
```
