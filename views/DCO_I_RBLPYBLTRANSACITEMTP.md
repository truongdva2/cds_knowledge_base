---
name: /DCO/I_RBLPYBLTRANSACITEMTP
description: Rblpybltransacitemtp
app_component: FIN-FSCM-COL-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FIN
  - FIN-FSCM
  - FIN-FSCM-COL
  - interface-view
  - transactional-processing
  - item-level
  - component:FIN-FSCM-COL-2CL
  - lob:Other
---
# /DCO/I_RBLPYBLTRANSACITEMTP

**Rblpybltransacitemtp**

| Property | Value |
|---|---|
| App Component | `FIN-FSCM-COL-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `createdAt: true } }` | `createdAt: true } }` |
| `CreationDateTime` | `CreationDateTime` |
| `DCoPaymentIsArranged` | `DCoPaymentIsArranged` |
| `PaymentDifferenceReason` | `PaymentDifferenceReason` |
| `CreditControlArea` | `CreditControlArea` |
| `_BusinessPartner` | *Association* |
| `_BranchAccount` | *Association* |
| `_Customer` | *Association* |
| `_CustomerCompany` | *Association* |
| `_PostingClass` | *Association* |
| `_PostingSubclassification` | *Association* |
| `_PaymentDifferenceReason` | *Association* |
| `_RblPyblDocRelationType` | *Association* |

## Associations

> No associations found.

## Source Code

```abap
@EndUserText: { label: 'Receivable Payable Transaction Item - TP' }
@AccessControl: { authorizationCheck: #MANDATORY,
                  personalData: { blocking: #BLOCKED_DATA_EXCLUDED } }
@VDM: { viewType: #TRANSACTIONAL,
        usage:    { type: [ #TRANSACTIONAL_PROCESSING_SERVICE ] } }
@ObjectModel: { usageType: { dataClass:      #TRANSACTIONAL,
                             sizeCategory:   #XL,
                             serviceQuality: #A },
                semanticKey: [ 'DCoAccountingDocumentID' ],
                representativeKey: 'RblPyblTransactionItemUUID',
                sapObjectNodeType: { name: 'DCoRblPyblTransactionItem' },
                modelingPattern:       #TRANSACTIONAL_INTERFACE,
                supportedCapabilities: [ #TRANSACTIONAL_PROVIDER,
                                         #CDS_MODELING_ASSOCIATION_TARGET,
                                         #CDS_MODELING_DATA_SOURCE,
                                         #UI_PROVIDER_PROJECTION_SOURCE ] }
@Metadata: { allowExtensions:             true,
             ignorePropagatedAnnotations: true }
@AbapCatalog: { extensibility: { extensible: true,
                                 dataSources: [ 'DCoRblPyblTransactionItem' ],
                                 elementSuffix: 'DRP',
                                 quota: { maximumFields: 500,
                                          maximumBytes: 5000 },
                                 allowNewCompositions: false } }

define root view entity /DCO/I_RblPyblTransacItemTP
  provider contract transactional_interface
  as projection on /DCO/R_RblPyblTransacItemTP as DCoRblPyblTransactionItem
                                                                                                  
{

      // Transaction information
      @Semantics: { uuid: true }
  key RblPyblTransactionItemUUID,

      @Semantics: { uuid: true }
      RblPyblTransactionUUID,

      @Semantics: { uuid: true }
      RblPyblPreviousTransactionUUID,

      @Semantics: { booleanIndicator: true }
      RblPyblItemIsObsolete,

      // Harmonized document information
      DCoAccountingDocumentID,
      ReceivablePayableDocument,
      ReceivablePayableDocumentItem,
      RblPyblDocumentSubitem,
      DCoAccountingDocumentOrigin,
      LogicalSystem,


      // AR/AP specific document information
      AccountingDocument,
      CompanyCode,
      FiscalYear,


      // Document dates
      DocumentDate,
      PostingDate,
      NetDueDate,


      // Master data
      Supplier,
      Customer,
      BusinessPartner,
      ContractAccount,
      CASubApplication,
      CAContract,
      BranchAccount,
      DCoAccountingDocumentBranch,


      // Transaction amount
      @Semantics: { amount: { currencyCode: 'TransactionCurrency' } }
      AmountInTransactionCurrency,

      TransactionCurrency,
      RblPyblTransactionType,


      // Cash discount
      @Semantics: { amount: { currencyCode: 'ClearingTransactionCurrency' } }
      CashDiscountAmtInClrgTransCrcy,

      @Semantics: { amount: { currencyCode: 'TransactionCurrency' } }
      CashDiscountBaseAmount,

      CashDiscount1DueDate,
      CashDiscount2DueDate,
      CashDiscount1Percent,
      CashDiscount2Percent,


      // Clearing Data
      @Semantics: { booleanIndicator: true }
      RblPyblItemIsCleared,

      ClearingDate,
      ClearingRblPyblDocument,
      RblPyblClearingReason,
      ClearingTransactionCurrency,

      @Semantics: { amount: { currencyCode: 'ClearingTransactionCurrency' } }
      ClrgBaseAmountInClrgTransCrcy,

      @Semantics: { amount: { currencyCode: 'ClearingTransactionCurrency' } }
      ClearingAmountInClrgTransCrcy,

      @Semantics: { amount: { currencyCode: 'ClearingTransactionCurrency' } }
      ResidualAmountInClrgTransCrcy,

      @Semantics: { amount: { currencyCode: 'ClearingTransactionCurrency' } }
      DeductionAmountInClrgTransCrcy,


      // Posting classification
      PostgClassfctnRblPyblCodeOrign,
      PostingClassification,
      PostgSubclfnRblPyblCodeOrigin,
      PostingSubclassification,


      // Additional information
      @Semantics: { text: true }
      ReceivablePayableItemText,

      @Semantics: { booleanIndicator: true }
      IsSalesRelated,

      @Semantics: { booleanIndicator: true }
      IsUsedInPaymentTransaction,


      // Business locks
      @Semantics: { booleanIndicator: true }
      RblPyblItemIsBlockedForDunning,

      @Semantics: { businessDate: { from: true } }
      DunningBlockStartDate,

      @Semantics: { businessDate: { to: true } }
      DunningBlockEndDate,

      @Semantics: { booleanIndicator: true }
      RblPyblItemIsBlockedForPayment,

      @Semantics: { businessDate: { from: true } }
      PaymentBlockStartDate,

      @Semantics: { businessDate: { to: true } }
      PaymentBlockEndDate,


      // Reference document
      OriginalReferenceDocument,
      ReferenceDocumentType,
      ReferenceDocumentLogicalSystem,


      // Original posting
      DCoOriglAccountingDocumentID,
      OriginalRblPyblDocument,
      OriginalRblPyblDocumentItem,
      OriginalRblPyblDocumentSubitem,

      @Semantics: { amount: { currencyCode: 'OriglItemTransactionCurrency' } }
      OriglRblPyblItmAmtInOrigItmTC,

      OriglItemTransactionCurrency,


      // Related posting
      @Semantics: { booleanIndicator: true }
      RblPyblItemHasRelations,

      RblPyblDocumentRelationType,
      RelatedRblPyblDocument,
      RelatedRblPyblDocumentItem,
      RelatedRblPyblDocumentSubitem,
      DCoRefAccountingDocumentID,


      // Additional references
      RblPyblDocumentReference,
      RblPyblAltvDocumentReference,
      FirstRblPyblAddlReference,
      SecondRblPyblAddlReference,
      ThirdRblPyblAddlReference,
      AssignmentReference,
      DCoPredecessorReceivableItem,
      RsdlItmPredecessorDocument,
      RsdlItmPredecessorDocumentItem,


      // Administrative data
      @Semantics: { systemDateTime: { lastChangedAt: true } }
      LastChangeDateTime,

      @Semantics: { systemDateTime: { createdAt: true } }
      CreationDateTime,

      // Debt collection specific fields
      DCoPaymentIsArranged,
      PaymentDifferenceReason,
      CreditControlArea,


      // Exposed associations
      _BusinessPartner,
      _BranchAccount,
      _Customer,
      _CustomerCompany,
      _PostingClass,
      _PostingSubclassification,
      _PaymentDifferenceReason,
      _RblPyblDocRelationType

}
```
