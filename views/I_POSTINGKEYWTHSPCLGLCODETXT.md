---
name: I_POSTINGKEYWTHSPCLGLCODETXT
description: Postingkeywthspclglcodetxt
app_component: FI-GL-GL-A-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FI
  - FI-GL
  - FI-GL-GL
  - interface-view
  - component:FI-GL-GL-A-2CL
  - lob:Finance
---
# I_POSTINGKEYWTHSPCLGLCODETXT

**Postingkeywthspclglcodetxt**

| Property | Value |
|---|---|
| App Component | `FI-GL-GL-A-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `Language` | `tbslt.spras` |
| `fis_bschl preserving type)` | `cast(tbsl.bschl` |
| `fac_umskz preserving type )` | `cast(tbslt.umskz` |
| `farp_koart preserving type )` | `cast (tbsl.koart` |
| `fis_bschl_name preserving type )` | `cast (tbslt.ltext` |
| `_Language` | *Association* |
| `_PostingKey` | *Association* |
| `_SpecialGLCode` | *Association* |
| `_FinancialAccountType` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_Language` | `I_Language` | [0..1] |
| `_PostingKey` | `I_PostingKey` | [0..1] |
| `_SpecialGLCode` | `I_SpecialGLCode` | [0..1] |
| `_FinancialAccountType` | `I_FinancialAccountType` | [0..1] |

## Source Code

```abap
@AbapCatalog.entityBuffer.definitionAllowed: true
 @AccessControl.authorizationCheck: #NOT_REQUIRED
@Analytics: { dataExtraction.enabled: true }
@EndUserText.label: 'Posting Key With Special General Leder Code - Text'
@ObjectModel: { dataCategory: #TEXT,
                representativeKey: 'SpecialGLCode'
,                usageType: { sizeCategory: #S,
                 dataClass:  #CUSTOMIZING,
                 serviceQuality: #A },
                supportedCapabilities: [#LANGUAGE_DEPENDENT_TEXT, #SQL_DATA_SOURCE, #CDS_MODELING_DATA_SOURCE, #CDS_MODELING_ASSOCIATION_TARGET, #EXTRACTION_DATA_SOURCE, #SEARCHABLE_ENTITY ],
                modelingPattern: #LANGUAGE_DEPENDENT_TEXT }                  
@Metadata.ignorePropagatedAnnotations: true 
@Search.searchable: true 
@VDM.viewType: #BASIC
@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API

define view entity I_PostingKeyWthSpclGLCodeTxt as select from tbslt inner join tbsl on tbslt.bschl = tbsl.bschl

association [0..1] to I_Language             as _Language             on $projection.Language = _Language.Language
association [0..1] to I_PostingKey           as _PostingKey           on  $projection.PostingKey    = _PostingKey.PostingKey 
association [0..1] to I_SpecialGLCode        as _SpecialGLCode        on $projection.SpecialGLCode = _SpecialGLCode.SpecialGLCode
                                                                      and $projection.FinancialAccountType = _SpecialGLCode.FinancialAccountType
association [0..1] to I_FinancialAccountType as _FinancialAccountType on $projection.FinancialAccountType = _FinancialAccountType.FinancialAccountType

{

@Semantics.language   
key tbslt.spras as Language,
@ObjectModel.foreignKey.association: '_PostingKey'
key cast(tbsl.bschl as fis_bschl preserving type) as PostingKey,
@ObjectModel.foreignKey.association: '_SpecialGLCode'
key cast(tbslt.umskz as fac_umskz preserving type ) as SpecialGLCode,
@ObjectModel.foreignKey.association: '_FinancialAccountType'
cast (tbsl.koart as farp_koart preserving type ) as FinancialAccountType, 
@Semantics.text
@Search.defaultSearchElement: true
@Search.fuzzinessThreshold: 0.8 
cast (tbslt.ltext as fis_bschl_name preserving type ) as PostingKeyName,

_Language,
_PostingKey,
_SpecialGLCode,
_FinancialAccountType

};
```
