---
name: I_PARKEDOPLACCTGDOCPYBLSITEM
description: Parkedoplacctgdocpyblsitem
app_component: FI-GL-IS-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FI
  - FI-GL
  - FI-GL-IS
  - interface-view
  - item-level
  - component:FI-GL-IS-2CL
  - lob:Finance
---
# I_PARKEDOPLACCTGDOCPYBLSITEM

**Parkedoplacctgdocpyblsitem**

| Property | Value |
|---|---|
| App Component | `FI-GL-IS-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `currencyCode: 'TransactionCurrency'} }` | `currencyCode: 'TransactionCurrency'} }` |
| `abap.curr( 23,2))` | `case shkzg  when 'H' then cast( cast(-wskto` |
| `abap.curr( 23,2))` | `else cast( cast( wskto` |
| `CashDiscountAmount` | `end` |
| `_AdditionalCurrency1` | *Association* |
| `_AdditionalCurrency2` | *Association* |
| `_AssetTransactionType` | *Association* |
| `_BusinessArea` | *Association* |
| `_ChartOfAccounts` | *Association* |
| `_CompanyCode` | *Association* |
| `_CompanyCodeCurrency` | *Association* |
| `_ControllingArea` | *Association* |
| `_CostCenter` | *Association* |
| `_Supplier` | *Association* |
| `_SupplierCompany` | *Association* |
| `_DebitCreditCode` | *Association* |
| `_FinancialAccountType` | *Association* |
| `_FixedAsset` | *Association* |
| `_GLAccountInChartOfAccounts` | *Association* |
| `_InternalOrder` | *Association* |
| `_MasterFixedAsset` | *Association* |
| `_Order` | *Association* |
| `_ParkedOplAcctgDocument` | *Association* |
| `_PartnerBusinessArea` | *Association* |
| `_PaymentCurrency` | *Association* |
| `_PostingKey` | *Association* |
| `_ProfitCenter` | *Association* |
| `_ProjectNetwork` | *Association* |
| `_SalesDocument` | *Association* |
| `_SalesDocumentItem` | *Association* |
| `_Segment` | *Association* |
| `_SourceCompanyCode` | *Association* |
| `_SourceFiscalYear` | *Association* |
| `_SourceParkedOplAcctgDocument` | *Association* |
| `_SpecialGLCode` | *Association* |
| `_TaxCountry` | *Association* |
| `_TaxType` | *Association* |
| `_TransactionCurrency` | *Association* |
| `_WBSElementBasicData` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_SourceCompanyCode` | `I_CompanyCode` | [0..1] |
| `_CompanyCode` | `I_CompanyCode` | [0..1] |
| `_SourceFiscalYear` | `I_FiscalYearForCompanyCode` | [0..1] |
| `_ParkedOplAcctgDocument` | `I_ParkedOplAcctgDocument` | [0..1] |
| `_SourceParkedOplAcctgDocument` | `I_ParkedOplAcctgDocument` | [0..1] |
| `_Supplier` | `I_Supplier` | [0..1] |
| `_SupplierCompany` | `I_SupplierCompany` | [0..1] |
| `_FinancialAccountType` | `I_FinancialAccountType` | [0..1] |
| `_PostingKey` | `I_PostingKey` | [0..1] |
| `_DebitCreditCode` | `I_DebitCreditCode` | [0..1] |
| `_SpecialGLCode` | `I_SpecialGLCode` | [0..1] |
| `_BusinessArea` | `I_BusinessArea` | [0..1] |
| `_PartnerBusinessArea` | `I_BusinessArea` | [0..1] |
| `_TaxCountry` | `I_Country` | [0..1] |
| `_TaxType` | `I_TaxType` | [0..1] |
| `_AssetTransactionType` | `I_AssetTransactionType` | [0..1] |
| `_ChartOfAccounts` | `I_ChartOfAccounts` | [0..1] |
| `_GLAccountInChartOfAccounts` | `I_GLAccountInChartOfAccounts` | [0..1] |
| `_InternalOrder` | `I_InternalOrder` | [0..1] |
| `_Order` | `I_Order` | [0..1] |
| `_ControllingArea` | `I_ControllingArea` | [0..1] |
| `_CostCenter` | `I_CostCenter` | [0..*] |
| `_MasterFixedAsset` | `I_MasterFixedAsset` | [0..1] |
| `_FixedAsset` | `I_FixedAsset` | [0..1] |
| `_ProfitCenter` | `I_ProfitCenter` | [0..*] |
| `_WBSElementBasicData` | `I_WBSElementBasicData` | [0..1] |
| `_ProjectNetwork` | `I_ProjectNetwork` | [0..1] |
| `_Segment` | `I_Segment` | [0..1] |
| `_SalesDocument` | `I_SalesDocument` | [0..1] |
| `_SalesDocumentItem` | `I_SalesDocumentItem` | [0..1] |
| `_CompanyCodeCurrency` | `I_Currency` | [1..1] |
| `_TransactionCurrency` | `I_Currency` | [0..1] |
| `_AdditionalCurrency1` | `I_Currency` | [0..1] |
| `_AdditionalCurrency2` | `I_Currency` | [0..1] |
| `_PaymentCurrency` | `I_Currency` | [0..1] |

## Source Code

```abap
@EndUserText.label: 'Parked Accounts Payable Item'
@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API
@VDM.viewType: #BASIC
@AccessControl.authorizationCheck: #MANDATORY
@AccessControl.personalData.blocking: #BLOCKED_DATA_EXCLUDED
@Analytics.internalName: #LOCAL
@Analytics: { 
      dataCategory: #DIMENSION, 
      dataExtraction: {
        enabled: true,
        delta.changeDataCapture: {
          mapping: 
            [
              { 
                table: 'VBSEGK',
                role: #MAIN,
                viewElement: ['SourceCompanyCode', 'SourceAccountingDocument', 'SourceFiscalYear', 'ParkedAcctgDocPyblsItem'],
                tableElement: ['ausbk', 'belnr', 'gjahr', 'bzkey']
              }
            ] 
         }
      }
    }
@Metadata.ignorePropagatedAnnotations: true
@Metadata.allowExtensions:true
@ObjectModel.representativeKey: 'ParkedAcctgDocPyblsItem'
@ObjectModel.usageType.sizeCategory: #M
@ObjectModel.usageType.dataClass:  #MIXED
@ObjectModel.usageType.serviceQuality: #D

@ObjectModel.modelingPattern: #ANALYTICAL_DIMENSION
@ObjectModel.supportedCapabilities: [#ANALYTICAL_DIMENSION, 
                                     #CDS_MODELING_ASSOCIATION_TARGET, 
                                     #CDS_MODELING_DATA_SOURCE,
                                     #EXTRACTION_DATA_SOURCE,
                                     #SQL_DATA_SOURCE]

@ObjectModel.sapObjectNodeType.name: 'ParkedOplAcctgDocPyblsItem' 

define view entity I_ParkedOplAcctgDocPyblsItem
  as select from vbsegk // p_vbsegk_com

  association [0..1] to I_CompanyCode                  as _SourceCompanyCode              on $projection.SourceCompanyCode               = _SourceCompanyCode.CompanyCode
  association [0..1] to I_CompanyCode                  as _CompanyCode                    on $projection.CompanyCode                     = _CompanyCode.CompanyCode
  association [0..1] to I_FiscalYearForCompanyCode     as _SourceFiscalYear               on $projection.SourceFiscalYear                = _SourceFiscalYear.FiscalYear
                                                                                         and $projection.SourceCompanyCode               = _SourceFiscalYear.CompanyCode
  association [0..1] to I_ParkedOplAcctgDocument       as _ParkedOplAcctgDocument         on $projection.SourceCompanyCode               = _ParkedOplAcctgDocument.SourceCompanyCode
                                                                                         and $projection.CompanyCode                     = _ParkedOplAcctgDocument.CompanyCode
                                                                                         and $projection.SourceAccountingDocument        = _ParkedOplAcctgDocument.SourceAccountingDocument
                                                                                         and $projection.SourceFiscalYear                = _ParkedOplAcctgDocument.SourceFiscalYear
  association [0..1] to I_ParkedOplAcctgDocument       as _SourceParkedOplAcctgDocument   on $projection.SourceCompanyCode               = _SourceParkedOplAcctgDocument.SourceCompanyCode
                                                                                         and $projection.SourceCompanyCode               = _SourceParkedOplAcctgDocument.CompanyCode
                                                                                         and $projection.SourceAccountingDocument        = _SourceParkedOplAcctgDocument.SourceAccountingDocument
                                                                                         and $projection.SourceFiscalYear                = _SourceParkedOplAcctgDocument.SourceFiscalYear
  association [0..1] to I_Supplier                     as _Supplier                       on $projection.Supplier                        = _Supplier.Supplier
  association [0..1] to I_SupplierCompany              as _SupplierCompany                on $projection.Supplier                        = _SupplierCompany.Supplier
                                                                                         and $projection.CompanyCode                     = _SupplierCompany.CompanyCode
  association [0..1] to I_FinancialAccountType         as _FinancialAccountType           on $projection.FinancialAccountType            = _FinancialAccountType.FinancialAccountType
  association [0..1] to I_PostingKey                   as _PostingKey                     on $projection.PostingKey                      = _PostingKey.PostingKey
  association [0..1] to I_DebitCreditCode              as _DebitCreditCode                on $projection.DebitCreditCode                 = _DebitCreditCode.DebitCreditCode
  association [0..1] to I_SpecialGLCode                as _SpecialGLCode                  on $projection.SpecialGLCode                   = _SpecialGLCode.SpecialGLCode
                                                                                         and $projection.FinancialAccountType            = _SpecialGLCode.FinancialAccountType
  association [0..1] to I_BusinessArea                 as _BusinessArea                   on $projection.BusinessArea                    = _BusinessArea.BusinessArea
  association [0..1] to I_BusinessArea                 as _PartnerBusinessArea            on $projection.PartnerBusinessArea             = _PartnerBusinessArea.BusinessArea
  association [0..1] to I_Country                      as _TaxCountry                     on $projection.TaxCountry                      = _TaxCountry.Country
  association [0..1] to I_TaxType                      as _TaxType                        on $projection.TaxType                         = _TaxType.TaxType
  association [0..1] to I_AssetTransactionType         as _AssetTransactionType           on $projection.AssetTransactionType            = _AssetTransactionType.AssetTransactionType
  association [0..1] to I_ChartOfAccounts              as _ChartOfAccounts                on $projection.ChartOfAccounts                 = _ChartOfAccounts.ChartOfAccounts
  association [0..1] to I_GLAccountInChartOfAccounts   as _GLAccountInChartOfAccounts     on $projection.ChartOfAccounts                 = _GLAccountInChartOfAccounts.ChartOfAccounts
                                                                                         and $projection.GLAccount                       = _GLAccountInChartOfAccounts.GLAccount  
  association [0..1] to I_InternalOrder                as _InternalOrder                  on $projection.OrderID                         = _InternalOrder.InternalOrder
  association [0..1] to I_Order                        as _Order                          on $projection.OrderID                         = _Order.OrderID
  association [0..1] to I_ControllingArea              as _ControllingArea                on $projection.ControllingArea                 = _ControllingArea.ControllingArea
  association [0..*] to I_CostCenter                   as _CostCenter                     on $projection.ControllingArea                 = _CostCenter.ControllingArea
                                                                                         and $projection.CostCenter                      = _CostCenter.CostCenter
  association [0..1] to I_MasterFixedAsset             as _MasterFixedAsset               on $projection.CompanyCode                     = _MasterFixedAsset.CompanyCode
                                                                                         and $projection.MasterFixedAsset                = _MasterFixedAsset.MasterFixedAsset
  association [0..1] to I_FixedAsset                   as _FixedAsset                     on $projection.CompanyCode                     = _FixedAsset.CompanyCode
                                                                                         and $projection.MasterFixedAsset                = _FixedAsset.MasterFixedAsset
                                                                                         and $projection.FixedAsset                      = _FixedAsset.FixedAsset
  association [0..*] to I_ProfitCenter                 as _ProfitCenter                   on $projection.ControllingArea                 = _ProfitCenter.ControllingArea
                                                                                         and $projection.ProfitCenter                    = _ProfitCenter.ProfitCenter
  association [0..1] to I_WBSElementBasicData          as _WBSElementBasicData            on $projection.WBSElementInternalID            = _WBSElementBasicData.WBSElementInternalID
  association [0..1] to I_ProjectNetwork               as _ProjectNetwork                 on $projection.ProjectNetwork                  = _ProjectNetwork.ProjectNetwork
  association [0..1] to I_Segment                      as _Segment                        on $projection.Segment                         = _Segment.Segment
  association [0..1] to I_SalesDocument                as _SalesDocument                  on $projection.SalesDocument                   = _SalesDocument.SalesDocument
  association [0..1] to I_SalesDocumentItem            as _SalesDocumentItem              on $projection.SalesDocument                   = _SalesDocumentItem.SalesDocument
                                                                                         and $projection.SalesDocumentItem               = _SalesDocumentItem.SalesDocumentItem
  association [1..1] to I_Currency                     as _CompanyCodeCurrency            on $projection.CompanyCodeCurrency             = _CompanyCodeCurrency.Currency
  association [0..1] to I_Currency                     as _TransactionCurrency            on $projection.TransactionCurrency             = _TransactionCurrency.Currency
  association [0..1] to I_Currency                     as _AdditionalCurrency1            on $projection.AdditionalCurrency1             = _AdditionalCurrency1.Currency
  association [0..1] to I_Currency                     as _AdditionalCurrency2            on $projection.AdditionalCurrency2             = _AdditionalCurrency2.Currency
  association [0..1] to I_Currency                     as _PaymentCurrency                on $projection.PaymentCurrency                 = _PaymentCurrency.Currency

{
    @ObjectModel.foreignKey.association: '_SourceCompanyCode'
key ausbk                                                                                 as  SourceCompanyCode,
    @ObjectModel.foreignKey.association: '_ParkedOplAcctgDocument'
key cast( belnr as fis_belnr preserving type )                                            as  SourceAccountingDocument,
    @ObjectModel.foreignKey.association: '_SourceFiscalYear'
key cast( gjahr as fis_gjahr_no_conv preserving type )                                    as  SourceFiscalYear,
key cast( bzkey as fis_parked_ap_item preserving type )                                   as  ParkedAcctgDocPyblsItem,

    cast( buzei as fis_buzei preserving type )                                            as  SourceAccountingDocumentItem,

    @ObjectModel.foreignKey.association: '_FinancialAccountType'
    cast( 'K' as farp_koart preserving type )                                             as  FinancialAccountType,

    @ObjectModel.foreignKey.association: '_CompanyCode'
    cast( bukrs as fis_bukrs preserving type )                                            as  CompanyCode,
    @ObjectModel.foreignKey.association: '_Supplier'
    cast( lifnr as fis_lifnr preserving type )                                            as  Supplier,
    @ObjectModel.foreignKey.association: '_PostingKey'
    cast( bschl as farp_bschl preserving type )                                           as  PostingKey,
    @ObjectModel.foreignKey.association: '_SpecialGLCode'
    cast( umskz as fac_umskz preserving type )                                            as  SpecialGLCode,
    cast( umsks as farp_umsks preserving type )                                           as  SpecialGLTransactionType,
    @ObjectModel.foreignKey.association: '_DebitCreditCode'
    cast( case xnegp when 'X' then case shkzg when 'S' then 'H'
                                              when 'H' then 'S'
                              end
                     else shkzg
    end as fis_shkzg preserving type )                                                    as  DebitCreditCode,
    @ObjectModel.foreignKey.association: '_BusinessArea'
    cast( gsber as fis_rbusa preserving type )                                            as  BusinessArea,
    @ObjectModel.foreignKey.association: '_PartnerBusinessArea'
    cast( vbsegk.pargb as fis_pargb preserving type )                                     as  PartnerBusinessArea,
    cast( mwskz as farp_mwskz preserving type )                                           as  TaxCode,
    @ObjectModel.foreignKey.association: '_TaxCountry'
    tax_country                                                                           as  TaxCountry,
    @ObjectModel.foreignKey.association: '_TaxType'
    mwart                                                                                 as  TaxType,
    valut                                                                                 as  ValueDate,
    cast( zuonr as fis_zuonr preserving type )                                            as  AssignmentReference,
    cast( sgtxt as farp_sgtxt )                                                           as  DocumentItemText,
    cast( bewar as fis_rmvct preserving type )                                            as  FinancialTransactionType,
    @ObjectModel.foreignKey.association: '_AssetTransactionType'
    cast( anbwa as fac_anbwa preserving type )                                            as  AssetTransactionType,
    cast( xumsw as farp_xumsw)                                                            as  IsSalesRelated,
    @ObjectModel.foreignKey.association: '_ChartOfAccounts'
    _CompanyCode.ChartOfAccounts                                                          as  ChartOfAccounts,
    @ObjectModel.foreignKey.association: '_GLAccountInChartOfAccounts'
    cast( hkont as fis_racct preserving type )                                            as  GLAccount,
    cast( filkd as farp_filkd preserving type )                                           as  BranchAccount,
    hzuon                                                                                 as  SpecialGLAccountAssignment,
    zfbdt                                                                                 as  DueCalculationBaseDate,
    cast( zterm as farp_dzterm preserving type )                                          as  PaymentTerms,
    cast( zbd1t as farp_dzbd1t preserving type )                                          as  CashDiscount1Days,
    cast( zbd2t as farp_dzbd2t preserving type )                                          as  CashDiscount2Days,
    cast( zbd3t as farp_dzbd3t preserving type )                                          as  NetPaymentDays,
    cast( zbd1p as dzbd1p_farp preserving type )                                          as  CashDiscount1Percent,
    cast( zbd2p as dzbd2p_farp preserving type )                                          as  CashDiscount2Percent,
    cast( zlsch as farp_schzw_bseg preserving type )                                      as  PaymentMethod,
    cast( zlspr as farp_dzlspr preserving type )                                          as  PaymentBlockingReason,
    cast( hbkid as farp_hbkid preserving type )                                           as  HouseBank,
    cast( rebzg as farp_rebzg preserving type )                                           as  InvoiceReference,
    cast( rebzj as fis_rebzj preserving type )                                            as  InvoiceReferenceFiscalYear,
    cast( rebzz as fis_rebzz preserving type )                                            as  InvoiceItemReference,
    cast( rebzt as farp_rebzt preserving type )                                           as  FollowOnDocumentType,
    cast( lzbkz as fac_lzbkz preserving type )                                            as  StateCentralBankPaymentReason,
    cast( landl as fac_landl preserving type )                                            as  SupplyingCountry,
    cast( samnr as farp_samnr preserving type )                                           as  InvoiceList,
    cast( mschl as farp_mschl preserving type )                                           as  DunningKey,
    cast( mansp as farp_mansp preserving type )                                           as  DunningBlockingReason,
    cast( madat as farp_madat preserving type )                                           as  LastDunningDate,
    cast( manst as farp_mahns_d preserving type )                                         as  DunningLevel,
    cast( maber as farp_maber preserving type )                                           as  DunningArea,
    cast( stceg as farp_stceg preserving type )                                           as  VATRegistration,
    cast( rstgr as farp_rstgr preserving type )                                           as  PaymentDifferenceReason,
    vname                                                                                 as  JointVenture,
    cast( egrup as jv_egroup_cds preserving type )                                        as  JointVentureEquityGroup,
    cast( vertt as fis_rantyp preserving type )                                           as  TreasuryContractType,
    cast( vertn as fis_ranl preserving type )                                             as  AssetContract,
    cast( vbewa as farp_sbewart preserving type )                                         as  CashFlowType,
    cast( fipos as fis_fipos_shortid preserving type )                                    as  CommitmentItemShortID,
    fdlev                                                                                 as  PlanningLevel,
    cast( uzawe as farp_uzawe preserving type )                                           as  PaymentMethodSupplement,
    cast( fistl as farp_fistl preserving type )                                           as  FundsCenter,
    geber                                                                                 as  Fund,
    cast( xref1 as farp_xref1 preserving type )                                           as  Reference1IDByBusinessPartner,
    cast( xref2 as farp_xref2 preserving type )                                           as  Reference2IDByBusinessPartner,
    cast( xref3 as fis_xref3 preserving type )                                            as  Reference3IDByBusinessPartner,
    cast( dtws1 as farp_dtat16 preserving type )                                          as  DataExchangeInstruction1,
    cast( dtws2 as farp_dtat17 preserving type )                                          as  DataExchangeInstruction2,
    cast( dtws3 as farp_dtat18 preserving type )                                          as  DataExchangeInstruction3,
    cast( dtws4 as farp_dtat19 preserving type )                                          as  DataExchangeInstruction4,
    cast( empfb as fis_empfb preserving type )                                            as  AlternativePayeePayer,
    cast( kidno as farp_kidno preserving type )                                           as  PaymentReference,
    cast( bupla as farp_bupla preserving type )                                           as  BusinessPlace,
    cast( secco as fis_secco preserving type )                                            as  TaxSection,
    grant_nbr                                                                             as  GrantID,
    cast( fkber as fm_farea preserving type )                                             as  FunctionalArea,
    cast( xnegp as farp_xnegp preserving type )                                           as  IsNegativePosting,
    cast( hktid as fac_hktid preserving type )                                            as  HouseBankAccount,
    cast( netdt as farp_netdt preserving type )                                           as  NetDueDate,
    cast( sk1dt as farp_sk1dt preserving type )                                           as  CashDiscount1DueDate,
    cast( sk2dt as farp_sk2dt preserving type )                                           as  CashDiscount2DueDate,
    j_1tpbupl                                                                             as  BranchCode,
    budget_pd                                                                             as  BudgetPeriod,
    cast( bvtyp as farp_bvtyp preserving type )                                           as  BPBankAccountInternalID,
    cast( dabrz as farp_dabrbez preserving type )                                         as  SettlementReferenceDate,
    cast( payt_rsn as farp_payt_rsn preserving type )                                     as  PaymentReason,
    cast( zbfix as farp_dzbfix preserving type )                                          as  FixedCashDiscount,
    cast( btype as jv_bilind_cds preserving type )                                        as  JointVentureBillingType,
    qsskz                                                                                 as  WithholdingTaxCode,
    @ObjectModel.foreignKey.association: '_ControllingArea'
    cast( kokrs as fis_kokrs preserving type )                                            as  ControllingArea,
    @ObjectModel.foreignKey.association: '_CostCenter'
    cast( kostl as fis_kostl preserving type )                                            as  CostCenter,
    @ObjectModel.foreignKey.association: '_Order'
    cast( aufnr as aufnr preserving type )                                                as  OrderID,
    cast( aufpl  as fis_ordintbillofops   preserving type )                               as  OrderInternalBillOfOperations,
    @ObjectModel.foreignKey.association: '_MasterFixedAsset'
    cast( anln1 as fis_anln1 preserving type )                                            as  MasterFixedAsset,
    @ObjectModel.foreignKey.association: '_FixedAsset'
    cast( anln2 as fis_anln2 preserving type )                                            as  FixedAsset,
    @ObjectModel.foreignKey.association: '_WBSElementBasicData'
    cast( ps_psp_pnr as fis_wbsint_no_conv preserving type )                              as  WBSElementInternalID,
    @ObjectModel.foreignKey.association: '_ProjectNetwork'
    nplnr                                                                                 as  ProjectNetwork,
    @ObjectModel.foreignKey.association: '_ProfitCenter'
    cast( prctr as fis_prctr preserving type )                                            as  ProfitCenter,
    @ObjectModel.foreignKey.association: '_Segment'
    segment                                                                               as  Segment,
    @ObjectModel.foreignKey.association: '_SalesDocument'
    kdauf                                                                                 as  SalesDocument,
    @ObjectModel.foreignKey.association: '_SalesDocumentItem'
    kdpos                                                                                 as  SalesDocumentItem,
    cast( erlkz as fis_kblerlkz preserving type )                                         as  LineItemIsCompleted,
    kdein                                                                                 as  ScheduleLine,
    cast( zumsk as fis_dzumsk preserving type )                                           as  TargetSpecialGLCode,

    @ObjectModel.foreignKey.association: '_PaymentCurrency'
    cast( pycur as farp_pycur preserving type )                                           as  PaymentCurrency,
    @ObjectModel.foreignKey.association: '_TransactionCurrency'
    _ParkedOplAcctgDocument.TransactionCurrency                                           as  TransactionCurrency,
    @ObjectModel.foreignKey.association: '_CompanyCodeCurrency'
    _ParkedOplAcctgDocument.CompanyCodeCurrency                                           as  CompanyCodeCurrency,
    @ObjectModel.foreignKey.association: '_AdditionalCurrency1'
    _ParkedOplAcctgDocument.AdditionalCurrency1                                           as  AdditionalCurrency1,
    @ObjectModel.foreignKey.association: '_AdditionalCurrency2'
    _ParkedOplAcctgDocument.AdditionalCurrency2                                           as  AdditionalCurrency2,

    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'CompanyCodeCurrency'} }
    case shkzg  when 'H' then cast( cast(-dmbtr as abap.curr( 23,2)) as fis_hsl preserving type )                      
                         else cast( cast( dmbtr as abap.curr( 23,2)) as fis_hsl preserving type )
    end                                                                                   as  AmountInCompanyCodeCurrency,
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-wrbtr as abap.curr( 23,2)) as fis_wsl preserving type )
                         else cast( cast( wrbtr as abap.curr( 23,2)) as fis_wsl preserving type )
    end                                                                                   as  AmountInTransactionCurrency,     
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'AdditionalCurrency1'} }
    case shkzg  when 'H' then cast( cast(-dmbe2 as abap.curr( 23,2)) as fis_dmbe2 preserving type )
                         else cast( cast( dmbe2 as abap.curr( 23,2)) as fis_dmbe2 preserving type )
    end                                                                                   as  AmountInAdditionalCurrency1,          
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'AdditionalCurrency2'} }
    case shkzg  when 'H' then cast( cast(-dmbe3 as abap.curr( 23,2)) as fis_dmbe3 preserving type )
                         else cast( cast( dmbe3 as abap.curr( 23,2)) as fis_dmbe3 preserving type )
    end                                                                                   as  AmountInAdditionalCurrency2,
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'CompanyCodeCurrency'} }
    case shkzg  when 'H' then cast( cast(-hwbas as abap.curr( 23,2)) as hwbas_shl preserving type )
                         else cast( cast( hwbas as abap.curr( 23,2)) as hwbas_shl preserving type )
    end                                                                                   as  TaxBaseAmountInCoCodeCrcy,   
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-fwbas as abap.curr( 23,2)) as fwbas_shl preserving type )
                         else cast( cast( fwbas as abap.curr( 23,2)) as fwbas_shl preserving type )
    end                                                                                   as  TaxBaseAmountInTransCrcy,  
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'CompanyCodeCurrency'} }
    case shkzg  when 'H' then cast( cast(-mwsts as abap.curr( 23,2)) as fis_mwsts preserving type )
                         else cast( cast( mwsts as abap.curr( 23,2)) as fis_mwsts preserving type )
    end                                                                                   as  TaxAmountInCoCodeCrcy,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-wmwst as abap.curr( 23,2)) as wmwst_shl preserving type )
                         else cast( cast( wmwst as abap.curr( 23,2)) as wmwst_shl preserving type )
    end                                                                                   as  TaxAmount,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'AdditionalCurrency1'} }
    case shkzg  when 'H' then cast( cast(-mwst2 as abap.curr( 23,2)) as fis_mwst2 preserving type )
                         else cast( cast( mwst2 as abap.curr( 23,2)) as fis_mwst2 preserving type )
    end                                                                                   as  TaxAmountInAdditionalCurrency1,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'AdditionalCurrency2'} }
    case shkzg  when 'H' then cast( cast(-mwst3 as abap.curr( 23,2)) as fis_mwst3 preserving type )
                         else cast( cast( mwst3 as abap.curr( 23,2)) as fis_mwst3 preserving type )
    end                                                                                   as  TaxAmountInAdditionalCurrency2,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-qbshb as abap.curr( 23,2)) as fis_qbshb preserving type )
                         else cast( cast( qbshb as abap.curr( 23,2)) as fis_qbshb preserving type )
    end                                                                                   as  WithholdingTaxAmount,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-qsshb as abap.curr( 23,2)) as fis_qsshb preserving type )
                         else cast( cast( qsshb as abap.curr( 23,2)) as fis_qsshb preserving type )
    end                                                                                   as  WithholdingTaxBaseAmount,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-qsfbt as abap.curr( 23,2)) as fis_qsfbt preserving type )
                         else cast( cast( qsfbt as abap.curr( 23,2)) as fis_qsfbt preserving type )
    end                                                                                   as  WithholdingTaxExemptionAmt,
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-fdwbt as abap.curr( 23,2)) as fis_fdwbt preserving type )
                         else cast( cast( fdwbt as abap.curr( 23,2)) as fis_fdwbt preserving type )
    end                                                                                   as  PlannedAmtInTransactionCrcy,   
    @DefaultAggregation: #SUM
    @Semantics: { amount : {currencyCode: 'PaymentCurrency'} }
    case shkzg  when 'H' then cast( cast(-pyamt as abap.curr( 23,2)) as pyamt_farp preserving type )
                         else cast( cast( pyamt as abap.curr( 23,2)) as pyamt_farp preserving type )
    end                                                                                   as  AmountInPaymentCurrency,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-skfbt as abap.curr( 23,2)) as fis_skfbt preserving type )
                         else cast( cast( skfbt as abap.curr( 23,2)) as fis_skfbt preserving type )
    end                                                                                   as  CashDiscountBaseAmount,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'CompanyCodeCurrency'} }
    case shkzg  when 'H' then cast( cast(-sknto as abap.curr( 23,2)) as fis_sknto preserving type )
                         else cast( cast( sknto as abap.curr( 23,2)) as fis_sknto preserving type )
    end                                                                                   as  CashDiscountAmtInCoCodeCrcy,
    @DefaultAggregation:#SUM
    @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
    case shkzg  when 'H' then cast( cast(-wskto as abap.curr( 23,2)) as fis_wskto preserving type )
                         else cast( cast( wskto as abap.curr( 23,2)) as fis_wskto preserving type )
    end                                                                                   as  CashDiscountAmount,
    
    _AdditionalCurrency1,
    _AdditionalCurrency2,   
    _AssetTransactionType,
    _BusinessArea,
    _ChartOfAccounts,
    _CompanyCode,
    _CompanyCodeCurrency,
    _ControllingArea,
    _CostCenter,
    _Supplier,
    _SupplierCompany,
    _DebitCreditCode,
    _FinancialAccountType,
    _FixedAsset,
    _GLAccountInChartOfAccounts,
    _InternalOrder,
    _MasterFixedAsset,
    _Order,
    _ParkedOplAcctgDocument,
    _PartnerBusinessArea,
    _PaymentCurrency,
    _PostingKey,
    _ProfitCenter,
    _ProjectNetwork,
    _SalesDocument,
    _SalesDocumentItem,
    _Segment,
    _SourceCompanyCode,
    _SourceFiscalYear,
    _SourceParkedOplAcctgDocument,
    _SpecialGLCode,
    _TaxCountry,
    _TaxType,
    _TransactionCurrency,
    _WBSElementBasicData
}
```
