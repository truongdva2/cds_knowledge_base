---
name: I_BILLINGDOCREQITEMPRCGELMNT
description: Billing DocumentREQITEMPRCGELMNT
app_component: SD-BIL-BDR-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - SD
  - SD-BIL
  - SD-BIL-BDR
  - interface-view
  - billing-document
  - billing
  - item-level
  - component:SD-BIL-BDR-2CL
  - lob:Sales & Distribution
---
# I_BILLINGDOCREQITEMPRCGELMNT

**Billing DocumentREQITEMPRCGELMNT**

| Property | Value |
|---|---|
| App Component | `SD-BIL-BDR-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `status: #DEPRECATED, successor: 'PriceConditionDeterminationDte' }` | `status: #DEPRECATED, successor: 'PriceConditionDeterminationDte' }` |
| `PricingElement.PricingDateTime` | `PricingElement.PricingDateTime` |
| `PricingElement.PriceConditionDeterminationDte` | `PricingElement.PriceConditionDeterminationDte` |
| `PricingElement.ConditionCalculationType` | `PricingElement.ConditionCalculationType` |
| `PricingElement.ConditionBaseValue` | `PricingElement.ConditionBaseValue` |
| `PricingElement.ConditionRateValue` | `PricingElement.ConditionRateValue` |
| `PricingElement.ConditionCurrency` | `PricingElement.ConditionCurrency` |
| `PricingElement.ConditionQuantity` | `PricingElement.ConditionQuantity` |
| `PricingElement.ConditionQuantityUnit` | `PricingElement.ConditionQuantityUnit` |
| `PricingElement.ConditionCategory` | `PricingElement.ConditionCategory` |
| `PricingElement.ConditionIsForStatistics` | `PricingElement.ConditionIsForStatistics` |
| `PricingElement.PricingScaleType` | `PricingElement.PricingScaleType` |
| `PricingElement.IsRelevantForAccrual` | `PricingElement.IsRelevantForAccrual` |
| `PricingElement.CndnIsRelevantForInvoiceList` | `PricingElement.CndnIsRelevantForInvoiceList` |
| `PricingElement.ConditionOrigin` | `PricingElement.ConditionOrigin` |
| `IsGroupCondition` | `PricingElement.IsGroupCondition_2` |
| `PricingElement.ConditionRecord` | `PricingElement.ConditionRecord` |
| `PricingElement.ConditionSequentialNumber` | `PricingElement.ConditionSequentialNumber` |
| `PricingElement.TaxCode` | `PricingElement.TaxCode` |
| `PricingElement.WithholdingTaxCode` | `PricingElement.WithholdingTaxCode` |
| `PricingElement.CndnRoundingOffDiffAmount` | `PricingElement.CndnRoundingOffDiffAmount` |
| `PricingElement.ConditionAmount` | `PricingElement.ConditionAmount` |
| `PricingElement.TransactionCurrency` | `PricingElement.TransactionCurrency` |
| `PricingElement.ConditionControl` | `PricingElement.ConditionControl` |
| `PricingElement.ConditionInactiveReason` | `PricingElement.ConditionInactiveReason` |
| `PricingElement.ConditionClass` | `PricingElement.ConditionClass` |
| `PricingElement.PrcgProcedureCounterForHeader` | `PricingElement.PrcgProcedureCounterForHeader` |
| `PricingElement.FactorForConditionBasisValue` | `PricingElement.FactorForConditionBasisValue` |
| `PricingElement.StructureCondition` | `PricingElement.StructureCondition` |
| `PricingElement.PeriodFactorForCndnBasisValue` | `PricingElement.PeriodFactorForCndnBasisValue` |
| `PricingElement.PricingScaleBasis` | `PricingElement.PricingScaleBasis` |
| `PricingElement.ConditionScaleBasisValue` | `PricingElement.ConditionScaleBasisValue` |
| `PricingElement.ConditionScaleBasisUnit` | `PricingElement.ConditionScaleBasisUnit` |
| `PricingElement.ConditionScaleBasisCurrency` | `PricingElement.ConditionScaleBasisCurrency` |
| `PricingElement.CndnIsRelevantForIntcoBilling` | `PricingElement.CndnIsRelevantForIntcoBilling` |
| `PricingElement.ConditionIsManuallyChanged` | `PricingElement.ConditionIsManuallyChanged` |
| `PricingElement.ConditionIsForConfiguration` | `PricingElement.ConditionIsForConfiguration` |
| `PricingElement.VariantCondition` | `PricingElement.VariantCondition` |
| `PricingElement.ConditionRateAmount` | `PricingElement.ConditionRateAmount` |
| `PricingElement.ConditionBaseAmount` | `PricingElement.ConditionBaseAmount` |
| `PricingElement.ConditionBaseQuantity` | `PricingElement.ConditionBaseQuantity` |
| `PricingElement.ConditionRateRatio` | `PricingElement.ConditionRateRatio` |
| `PricingElement.ConditionRateRatioUnit` | `PricingElement.ConditionRateRatioUnit` |
| `PricingElement.ConditionScaleBaseAmount` | `PricingElement.ConditionScaleBaseAmount` |
| `PricingElement.ConditionScaleBaseQuantity` | `PricingElement.ConditionScaleBaseQuantity` |
| `PricingElement.ConditionAbsoluteExchangeRate` | `PricingElement.ConditionAbsoluteExchangeRate` |
| `PricingElement.ConditionExchRateIsIndrctQtan` | `PricingElement.ConditionExchRateIsIndrctQtan` |
| `PricingElement.ConditionToBaseQtyNmrtr` | `PricingElement.ConditionToBaseQtyNmrtr` |
| `PricingElement.ConditionToBaseQtyDnmntr` | `PricingElement.ConditionToBaseQtyDnmntr` |
| `PricingElement.AccessNumberOfAccessSequence` | `PricingElement.AccessNumberOfAccessSequence` |
| `PricingElement.AccountKeyForGLAccount` | `PricingElement.AccountKeyForGLAccount` |
| `PricingElement.GLAccount` | `PricingElement.GLAccount` |
| `PricingElement.AccrualsGLAccount` | `PricingElement.AccrualsGLAccount` |
| `_BillingDocumentRequest` | *Association* |
| `_Item` | *Association* |
| `PricingElement._ConditionApplication` | `PricingElement._ConditionApplication` |
| `PricingElement._PricingConditionType, // naming could be better: _ConditionType` | `PricingElement._PricingConditionType, // naming could be better: _ConditionType` |
| `PricingElement._ConditionCalculationType` | `PricingElement._ConditionCalculationType` |
| `PricingElement._ConditionCurrency` | `PricingElement._ConditionCurrency` |
| `PricingElement._Currency, // naming could be better: _TransactionCurrency` | `PricingElement._Currency, // naming could be better: _TransactionCurrency` |
| `PricingElement._ConditionQuantityUnit` | `PricingElement._ConditionQuantityUnit` |
| `PricingElement._ConditionCategory` | `PricingElement._ConditionCategory` |
| `PricingElement._ConditionOrigin` | `PricingElement._ConditionOrigin` |
| `PricingElement._ConditionControl` | `PricingElement._ConditionControl` |
| `PricingElement._ConditionInactiveReason` | `PricingElement._ConditionInactiveReason` |
| `PricingElement._ConditionClass` | `PricingElement._ConditionClass` |
| `PricingElement._PricingScaleBasis` | `PricingElement._PricingScaleBasis` |
| `PricingElement._ScaleUnitOfMeasure, // naming could be better: _ConditionScaleBasisUnit` | `PricingElement._ScaleUnitOfMeasure, // naming could be better: _ConditionScaleBasisUnit` |
| `PricingElement._ScaleCurrency, // naming could be better: _ConditionScaleBasisCurrency` | `PricingElement._ScaleCurrency, // naming could be better: _ConditionScaleBasisCurrency` |
| `PricingElement._VariantCondition` | `PricingElement._VariantCondition` |
| `PricingElement._StructureCondition` | `PricingElement._StructureCondition` |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_BillingDocumentRequest` | `I_BillingDocumentRequest` | [1..1] |
| `_Item` | `I_BillingDocumentRequestItem` | [1..1] |

## Source Code

```abap
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AbapCatalog.sqlViewName: 'ISDBILDOCREQITPE'

@AccessControl.authorizationCheck: #CHECK
@AccessControl.personalData.blocking: #REQUIRED

@ClientHandling.algorithm: #SESSION_VARIABLE

@EndUserText.label: 'Billing Doc Req Item Pricing Element'

@Metadata.ignorePropagatedAnnotations: true

@ObjectModel.supportedCapabilities: [ #SQL_DATA_SOURCE, #CDS_MODELING_DATA_SOURCE, #CDS_MODELING_ASSOCIATION_TARGET ]
@ObjectModel.usageType: { dataClass: #TRANSACTIONAL, serviceQuality: #B, sizeCategory: #XL }

@VDM.viewType: #BASIC

define view I_BillingDocReqItemPrcgElmnt
  as select from I_BillingDocItemPrcgElmntBasic as PricingElement

  association [1..1] to I_BillingDocumentRequest     as _BillingDocumentRequest
    on $projection.BillingDocumentRequest = _BillingDocumentRequest.BillingDocumentRequest

  association [1..1] to I_BillingDocumentRequestItem as _Item
    on  $projection.BillingDocumentRequest     = _Item.BillingDocumentRequest
    and $projection.BillingDocumentRequestItem = _Item.BillingDocumentRequestItem

{
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BillingDocumentRequestStdVH',
                                                      element: 'BillingDocumentRequest' } } ]
      @ObjectModel.foreignKey.association: '_BillingDocumentRequest'
  key cast(PricingElement.BillingDocument as vbeln_bdr preserving type)     as BillingDocumentRequest,

      @ObjectModel.foreignKey.association: '_Item'
  key cast(PricingElement.BillingDocumentItem as posnr_bdr preserving type) as BillingDocumentRequestItem,

  key PricingElement.PricingProcedureStep,
  key PricingElement.PricingProcedureCounter, // decision from pricing colleagues: keep wrong name, do not rename to PricingElementCounter

      @ObjectModel.foreignKey.association: '_ConditionApplication'
      PricingElement.ConditionApplication, // required for foreign key association of condition type

      @ObjectModel.foreignKey.association: '_PricingConditionType'
      PricingElement.ConditionType,

      @VDM.lifecycle: { status: #DEPRECATED, successor: 'PriceConditionDeterminationDte' }
      PricingElement.PricingDateTime,

      PricingElement.PriceConditionDeterminationDte,

      @ObjectModel.foreignKey.association: '_ConditionCalculationType'
      PricingElement.ConditionCalculationType,

      PricingElement.ConditionBaseValue,
      PricingElement.ConditionRateValue,

      @ObjectModel.foreignKey.association: '_ConditionCurrency'
      PricingElement.ConditionCurrency,

      @DefaultAggregation: #NONE
      @Semantics.quantity.unitOfMeasure: 'ConditionQuantityUnit'
      PricingElement.ConditionQuantity,

      @ObjectModel.foreignKey.association: '_ConditionQuantityUnit'
      @Semantics.unitOfMeasure: true
      PricingElement.ConditionQuantityUnit,

      @ObjectModel.foreignKey.association: '_ConditionCategory'
      PricingElement.ConditionCategory,

      PricingElement.ConditionIsForStatistics,
      PricingElement.PricingScaleType,
      PricingElement.IsRelevantForAccrual,

      @Semantics.booleanIndicator: true
      PricingElement.CndnIsRelevantForInvoiceList,

      @ObjectModel.foreignKey.association: '_ConditionOrigin'
      PricingElement.ConditionOrigin,

      @Semantics.booleanIndicator: true
      PricingElement.IsGroupCondition_2                                     as IsGroupCondition,

      PricingElement.ConditionRecord,
      PricingElement.ConditionSequentialNumber,
      PricingElement.TaxCode,
      PricingElement.WithholdingTaxCode,

      @DefaultAggregation: #NONE
      @Semantics.amount.currencyCode: 'TransactionCurrency'
      PricingElement.CndnRoundingOffDiffAmount,

      @DefaultAggregation: #NONE
      @Semantics.amount.currencyCode: 'TransactionCurrency'
      PricingElement.ConditionAmount,

      @ObjectModel.foreignKey.association: '_Currency'
      @Semantics.currencyCode: true
      PricingElement.TransactionCurrency,

      @ObjectModel.foreignKey.association: '_ConditionControl'
      PricingElement.ConditionControl,

      @ObjectModel.foreignKey.association: '_ConditionInactiveReason'
      PricingElement.ConditionInactiveReason,

      @ObjectModel.foreignKey.association: '_ConditionClass'
      PricingElement.ConditionClass,

      PricingElement.PrcgProcedureCounterForHeader,
      PricingElement.FactorForConditionBasisValue,

      @ObjectModel.foreignKey.association: '_StructureCondition'
      PricingElement.StructureCondition,

      PricingElement.PeriodFactorForCndnBasisValue,

      @ObjectModel.foreignKey.association: '_PricingScaleBasis'
      PricingElement.PricingScaleBasis,

      PricingElement.ConditionScaleBasisValue,

      @ObjectModel.foreignKey.association: '_ScaleUnitOfMeasure'
      @Semantics.unitOfMeasure: true
      PricingElement.ConditionScaleBasisUnit,

      @ObjectModel.foreignKey.association: '_ScaleCurrency'
      @Semantics.currencyCode: true
      PricingElement.ConditionScaleBasisCurrency,

      PricingElement.CndnIsRelevantForIntcoBilling,
      PricingElement.ConditionIsManuallyChanged,
      PricingElement.ConditionIsForConfiguration,
      PricingElement.VariantCondition,

      @OData.v2.amount.noDecimalShift: true
      @Semantics.amount.currencyCode: 'ConditionCurrency'
      PricingElement.ConditionRateAmount,

      @OData.v2.amount.noDecimalShift: true
      @Semantics.amount.currencyCode: 'TransactionCurrency'
      PricingElement.ConditionBaseAmount,

      @Semantics.quantity.unitOfMeasure: 'ConditionQuantityUnit'
      PricingElement.ConditionBaseQuantity,

      @Semantics.quantity.unitOfMeasure: 'ConditionRateRatioUnit'
      PricingElement.ConditionRateRatio,

      @Semantics.unitOfMeasure: true
      PricingElement.ConditionRateRatioUnit,

      @OData.v2.amount.noDecimalShift: true
      @Semantics.amount.currencyCode: 'ConditionScaleBasisCurrency'
      PricingElement.ConditionScaleBaseAmount,

      @Semantics.quantity.unitOfMeasure: 'ConditionScaleBasisUnit'
      PricingElement.ConditionScaleBaseQuantity,

      PricingElement.ConditionAbsoluteExchangeRate,

      @Semantics.booleanIndicator: true
      PricingElement.ConditionExchRateIsIndrctQtan,

      PricingElement.ConditionToBaseQtyNmrtr,
      PricingElement.ConditionToBaseQtyDnmntr,
      PricingElement.AccessNumberOfAccessSequence,
      PricingElement.AccountKeyForGLAccount,
      PricingElement.GLAccount,
      PricingElement.AccrualsGLAccount,

      // Association
      @ObjectModel.association.type: [ #TO_COMPOSITION_ROOT ]
      _BillingDocumentRequest,

      @ObjectModel.association.type: [ #TO_COMPOSITION_PARENT ]
      _Item,

      PricingElement._ConditionApplication,
      PricingElement._PricingConditionType, // naming could be better: _ConditionType
      PricingElement._ConditionCalculationType,
      PricingElement._ConditionCurrency,
      PricingElement._Currency, // naming could be better: _TransactionCurrency
      PricingElement._ConditionQuantityUnit,
      PricingElement._ConditionCategory,
      PricingElement._ConditionOrigin,
      PricingElement._ConditionControl,
      PricingElement._ConditionInactiveReason,
      PricingElement._ConditionClass,
      PricingElement._PricingScaleBasis,
      PricingElement._ScaleUnitOfMeasure, // naming could be better: _ConditionScaleBasisUnit
      PricingElement._ScaleCurrency, // naming could be better: _ConditionScaleBasisCurrency
      PricingElement._VariantCondition,
      PricingElement._StructureCondition
}

where _BillingDocumentRequest.SDDocumentCategory = 'EBDR' // Billing Document Request
```
