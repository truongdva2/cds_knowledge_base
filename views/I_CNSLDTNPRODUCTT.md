---
name: I_CNSLDTNPRODUCTT
description: Cnsldtnproductt
app_component: FIN-CS-MD-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FIN
  - FIN-CS
  - FIN-CS-MD
  - interface-view
  - product
  - component:FIN-CS-MD-2CL
  - lob:Other
---
# I_CNSLDTNPRODUCTT

**Cnsldtnproductt**

| Property | Value |
|---|---|
| App Component | `FIN-CS-MD-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `status: #DEPRECATED, successor: 'ProductName' }` | `status: #DEPRECATED, successor: 'ProductName' }` |
| `fincs_description_text_40 preserving type )` | `cast( _Source.AdditionalMasterDataText` |
| `fincs_productname preserving type )` | `cast( _Source.AdditionalMasterDataText` |
| `_Source.CnsldtnIsAdditionalMasterData` | *Association* |
| `/* associations */` | `/* associations */` |
| `_Language` | *Association* |
| `_Product` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_Language` | `I_Language` | [1..1] |
| `_Product` | `I_CnsldtnProduct` | [1..1] |

## Source Code

```abap
@AbapCatalog:{
  sqlViewName: 'ICSPRODUCTT',
  compiler.compareFilter: true
  }
@Analytics: {
  dataExtraction.enabled: true
}
@AccessControl:{
  authorizationCheck: #MANDATORY
  }
@ClientHandling.algorithm: #SESSION_VARIABLE
@Metadata.ignorePropagatedAnnotations: true

@ObjectModel:{
  usageType: {
    dataClass: #MASTER,
    serviceQuality: #D,
    sizeCategory: #L},
  representativeKey: 'Product',
  dataCategory: #TEXT,
  modelingPattern: #LANGUAGE_DEPENDENT_TEXT,
  supportedCapabilities: [#CDS_MODELING_DATA_SOURCE,
                          #CDS_MODELING_ASSOCIATION_TARGET,
                          #SQL_DATA_SOURCE,
                          #EXTRACTION_DATA_SOURCE,
                          #LANGUAGE_DEPENDENT_TEXT ],
  sapObjectNodeType.name: 'CnsldtnProductText'
}
@VDM:{
  viewType: #COMPOSITE,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
}
@EndUserText.label: 'Consolidation Combined Product - Text'

define view I_CnsldtnProductT
  as select distinct from P_CnsldtnProductT as _Source

    inner join            I_CnsldtnProduct  as _Main on  _Main.Product                       = _Source.Product
                                                     and _Main.CnsldtnIsAdditionalMasterData = _Source.CnsldtnIsAdditionalMasterData

  association [1..1] to I_Language       as _Language on $projection.Language = _Language.Language

  association [1..1] to I_CnsldtnProduct as _Product  on $projection.Product = _Product.Product

{

      @ObjectModel.foreignKey.association: '_Language'
      @Semantics.language
  key _Source.Language,

      @ObjectModel.foreignKey.association: '_Product'
      @Consumption.valueHelpDefinition: [{
        entity: {
          name: 'I_CnsldtnProductVH',
          element: 'Product'
        }
      }]
  key cast( _Source.Product as fincs_product preserving type )                              as Product,

      @API.element: {releaseState: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData'}
      @VDM.lifecycle: {status: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData' }
      cast( _Source.AdditionalMasterDataSource as fincs_masterdatasource preserving type )  as AdditionalMasterDataSource,

      @Semantics.text
      @API.element: {releaseState: #DEPRECATED, successor: 'ProductName'}
      @VDM.lifecycle: {status: #DEPRECATED, successor: 'ProductName' }
      cast( _Source.AdditionalMasterDataText as fincs_description_text_40 preserving type ) as AdditionalMasterDataText,

      @Semantics.text
      cast( _Source.AdditionalMasterDataText as fincs_productname preserving type )         as ProductName,

      _Source.CnsldtnIsAdditionalMasterData,


      /* associations */
      _Language,
      _Product
}
where
  _Source.Language is not null
```
