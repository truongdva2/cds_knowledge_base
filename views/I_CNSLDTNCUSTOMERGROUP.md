---
name: I_CNSLDTNCUSTOMERGROUP
description: Cnsldtncustomergroup
app_component: FIN-CS-MD-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FIN
  - FIN-CS
  - FIN-CS-MD
  - interface-view
  - customer
  - component:FIN-CS-MD-2CL
  - lob:Other
  - bo:Customer
---
# I_CNSLDTNCUSTOMERGROUP

**Cnsldtncustomergroup**

| Property | Value |
|---|---|
| App Component | `FIN-CS-MD-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `status: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData' }` | `status: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData' }` |
| `fincs_masterdatasource preserving type )` | `cast( max ( AdditionalMasterDataSource )` |
| `fincs_isadditionalmasterdata preserving type )` | `cast( max ( CnsldtnIsAdditionalMasterData )` |
| `/* associations */` | `/* associations */` |
| `_Text` | *Association* |
| `_CustomerGroupHierNode` | *Association* |
| `_MDSource` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_Text` | `I_CnsldtnCustomerGroupT` | [0..*] |
| `_CustomerGroupHierNode` | `I_CnsldtnCustGroupHierNode` | [0..*] |
| `_MDSource` | `I_CnsldtnMDSource` | [1..1] |

## Source Code

```abap
@AbapCatalog:{
  sqlViewName: 'ICSCUSTOMERGP',
  compiler.compareFilter: true,
  viewEnhancementCategory: [ #PROJECTION_LIST, #GROUP_BY ]
  }
@Analytics: {
  dataCategory: #DIMENSION,
  internalName: #LOCAL,
  dataExtraction.enabled: true
}
@AccessControl:{
  authorizationCheck: #MANDATORY
  }
@ClientHandling.algorithm: #SESSION_VARIABLE
@Metadata:{
  allowExtensions:true,
  ignorePropagatedAnnotations: true
  }
@ObjectModel:{
  usageType: {
    dataClass: #MASTER,
    serviceQuality: #C,
    sizeCategory: #L},
  representativeKey: 'CustomerGroup',
  modelingPattern: #ANALYTICAL_DIMENSION,
  supportedCapabilities: [#CDS_MODELING_DATA_SOURCE,
                          #CDS_MODELING_ASSOCIATION_TARGET,
                          #SQL_DATA_SOURCE,
                          #EXTRACTION_DATA_SOURCE,
                          #ANALYTICAL_DIMENSION],
  sapObjectNodeType.name: 'CnsldtnCustomerGroup'
}
@VDM:{
  viewType: #COMPOSITE,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
  }
@EndUserText.label: 'Consolidation Combined Customer Group'

// C1 Released for use in Key User Apps & PaaS since CE2302

// expose only additional master data entity in case of homonymous non-additional master data entity existence

define view I_CnsldtnCustomerGroup
  as select distinct from P_CnsldtnCustomerGroup

  association [0..*] to I_CnsldtnCustomerGroupT    as _Text                  on $projection.CustomerGroup = _Text.CustomerGroup

  association [0..*] to I_CnsldtnCustGroupHierNode as _CustomerGroupHierNode on $projection.CustomerGroup = _CustomerGroupHierNode.CustomerGroup

  association [1..1] to I_CnsldtnMDSource          as _MDSource              on $projection.AdditionalMasterDataSource = _MDSource.AdditionalMasterDataSource

{

      @ObjectModel.text.association: '_Text'
      @ObjectModel.hierarchy.association: '_CustomerGroupHierNode'
      @Consumption.valueHelpDefinition: [{
        entity: {
          name: 'I_CnsldtnCustomerGroupVH',
          element: 'CustomerGroup'
        }
      }]
  key cast( CustomerGroup as fincs_customergroup preserving type )                                  as CustomerGroup,

      @ObjectModel.foreignKey.association: '_MDSource'
      @API.element: {releaseState: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData'}
      @VDM.lifecycle: {status: #DEPRECATED, successor: 'CnsldtnIsAdditionalMasterData' }
      cast( max ( AdditionalMasterDataSource ) as fincs_masterdatasource preserving type )          as AdditionalMasterDataSource,

      cast( max ( CnsldtnIsAdditionalMasterData ) as fincs_isadditionalmasterdata preserving type ) as CnsldtnIsAdditionalMasterData,


      /* associations */
      _Text,
      _CustomerGroupHierNode,
      _MDSource

}
where
  CustomerGroup is not initial //required to avoid access to corrupt database entries
group by
  CustomerGroup
```
