---
name: I_SERIALNUMBERDELIVERYDOCUMENT
description: Serial NumberDelivery DocumentUMENT
app_component: LO-MD-SN-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - LO
  - LO-MD
  - LO-MD-SN
  - interface-view
  - delivery-document
  - delivery
  - serial-number
  - document
  - component:LO-MD-SN-2CL
  - lob:Logistics General
  - bo:DeliveryDocument
---
# I_SERIALNUMBERDELIVERYDOCUMENT

**Serial NumberDelivery DocumentUMENT**

| Property | Value |
|---|---|
| App Component | `LO-MD-SN-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `key _MaintenanceObjectListItem.Equipment` | `_MaintenanceObjectListItem.Equipment` |
| `key _MaintObjListDeliveryDocument.DeliveryDocument` | `_MaintObjListDeliveryDocument.DeliveryDocument` |
| `key _MaintObjListDeliveryDocument.DeliveryDocumentItem` | `_MaintObjListDeliveryDocument.DeliveryDocumentItem` |
| `_MaintenanceObjectListItem.Material` | *Association* |
| `_MaintenanceObjectListItem.SerialNumber` | *Association* |
| `_MaintenanceObjectListItem._Equipment` | *Association* |
| `_MaintObjListDeliveryDocument._DeliveryDocument` | *Association* |
| `_MaintObjListDeliveryDocument._DeliveryDocumentItem` | *Association* |
| `_MaintenanceObjectListItem._Product` | *Association* |

## Associations

> No associations found.

## Source Code

```abap
@AccessControl: { authorizationCheck: #CHECK,
                  personalData.blocking: #NOT_REQUIRED }
                  
@VDM.viewType: #COMPOSITE

@ObjectModel.usageType: {
        dataClass:      #TRANSACTIONAL,
        serviceQuality: #A,
        sizeCategory:   #XL }
@ObjectModel.supportedCapabilities: [
  #SQL_DATA_SOURCE,
  #CDS_MODELING_DATA_SOURCE,
  #CDS_MODELING_ASSOCIATION_TARGET
]

@EndUserText.label: 'Serial Number in Delivery Document'
@Metadata.ignorePropagatedAnnotations:true
define view entity I_SerialNumberDeliveryDocument
  as select from I_MaintObjListDeliveryDocument as _MaintObjListDeliveryDocument
  -- data source association to list item cannot be used due to current CDS restrictions
  -- (could be done implicitly in field list by _A._B[inner].C, but non-maskable warnings ensue)
  -- ... join needs foreign key relationship to be defined again
    inner join I_MaintenanceObjectListItem as _MaintenanceObjectListItem
      on _MaintObjListDeliveryDocument.MaintenanceObjectList = _MaintenanceObjectListItem.MaintenanceObjectList
{
  @ObjectModel.foreignKey.association: '_Equipment'
  key _MaintenanceObjectListItem.Equipment,

  @ObjectModel.foreignKey.association: '_DeliveryDocument'
  key _MaintObjListDeliveryDocument.DeliveryDocument,
  @ObjectModel.foreignKey.association: '_DeliveryDocumentItem'
  key _MaintObjListDeliveryDocument.DeliveryDocumentItem,

  @ObjectModel.foreignKey.association: '_Product'
  _MaintenanceObjectListItem.Material,
  _MaintenanceObjectListItem.SerialNumber,

  _MaintenanceObjectListItem._Equipment,
  _MaintObjListDeliveryDocument._DeliveryDocument,
  _MaintObjListDeliveryDocument._DeliveryDocumentItem,
  _MaintenanceObjectListItem._Product
}
```
