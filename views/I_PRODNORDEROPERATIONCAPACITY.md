---
name: I_PRODNORDEROPERATIONCAPACITY
description: Prodnorderoperationcapacity
app_component: PP-VDM-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - PP
  - PP-VDM
  - interface-view
  - component:PP-VDM-2CL
  - lob:Manufacturing
---
# I_PRODNORDEROPERATIONCAPACITY

**Prodnorderoperationcapacity**

| Property | Value |
|---|---|
| App Component | `PP-VDM-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `name: 'I_CapacityStdVH', element: 'CapacityInternalID' } } ]` | `name: 'I_CapacityStdVH', element: 'CapacityInternalID' } } ]` |
| `CapReq.CapacityInternalID` | `CapReq.CapacityInternalID` |
| `CapReq.CapacityRequirementDistrKey` | `CapReq.CapacityRequirementDistrKey` |
| `CapReq.CapacityRequirementUnit` | `CapReq.CapacityRequirementUnit` |
| `CapReq.ScheduledCapReqOpSegSetupDurn` | `CapReq.ScheduledCapReqOpSegSetupDurn` |
| `CapReq.RemainingCapReqOpSegSetupDurn` | `CapReq.RemainingCapReqOpSegSetupDurn` |
| `CapReq.ScheduledCapReqOpSegProcgDurn` | `CapReq.ScheduledCapReqOpSegProcgDurn` |
| `CapReq.RemainingCapReqOpSegProcgDurn` | `CapReq.RemainingCapReqOpSegProcgDurn` |
| `CapReq.ScheduledCapReqOpSegTrdwnDurn` | `CapReq.ScheduledCapReqOpSegTrdwnDurn` |
| `CapReq.RemainingCapReqOpSegTrdwnDurn` | `CapReq.RemainingCapReqOpSegTrdwnDurn` |
| `CapReq.OperationEarliestStartDate` | `CapReq.OperationEarliestStartDate` |
| `CapReq.OperationEarliestStartTime` | `CapReq.OperationEarliestStartTime` |
| `CapReq.OperationEarliestEndDate` | `CapReq.OperationEarliestEndDate` |
| `CapReq.OperationEarliestEndTime` | `CapReq.OperationEarliestEndTime` |
| `CapReq.OperationLatestStartDate` | `CapReq.OperationLatestStartDate` |
| `CapReq.OperationLatestStartTime` | `CapReq.OperationLatestStartTime` |
| `CapReq.OperationLatestEndDate` | `CapReq.OperationLatestEndDate` |
| `CapReq.OperationLatestEndTime` | `CapReq.OperationLatestEndTime` |
| `CapReq.ActualStartDate` | `CapReq.ActualStartDate` |
| `CapReq.ActualStartTime` | `CapReq.ActualStartTime` |
| `CapReq.ActualEndDate` | `CapReq.ActualEndDate` |
| `CapReq.ActualEndTime` | `CapReq.ActualEndTime` |
| `CapReq.ErlstSchedldProcStrtDte` | `CapReq.ErlstSchedldProcStrtDte` |
| `CapReq.ErlstSchedldProcStrtTime` | `CapReq.ErlstSchedldProcStrtTime` |
| `CapReq.ErlstSchedldTrdwnStrtDte` | `CapReq.ErlstSchedldTrdwnStrtDte` |
| `CapReq.ErlstSchedldTrdwnStrtTime` | `CapReq.ErlstSchedldTrdwnStrtTime` |
| `CapReq.LtstSchedldProcStrtDte` | `CapReq.LtstSchedldProcStrtDte` |
| `CapReq.LtstSchedldProcStrtTime` | `CapReq.LtstSchedldProcStrtTime` |
| `CapReq.LtstSchedldTrdwnStrtDte` | `CapReq.LtstSchedldTrdwnStrtDte` |
| `CapReq.LtstSchedldTrdwnStrtTime` | `CapReq.LtstSchedldTrdwnStrtTime` |
| `CapReq.ActualNumberOfSplits` | `CapReq.ActualNumberOfSplits` |
| `CapReq.CapacityRequirementSplit` | `CapReq.CapacityRequirementSplit` |
| `CapReq.ObjectInternalID` | `CapReq.ObjectInternalID` |
| `_ProductionOrderHeader` | *Association* |
| `_ProductionOrderType` | *Association* |
| `_ProductionOrderSequence` | *Association* |
| `_ProductionOrderOperation` | *Association* |
| `_Plant` | *Association* |
| `_WorkCenter` | *Association* |
| `_WorkCenterType` | *Association* |
| `OrderOperation._OrderInternalID` | `OrderOperation._OrderInternalID` |
| `CapReq._CapacityRequirement` | `CapReq._CapacityRequirement` |
| `CapReq._CapacityRequirementItem` | `CapReq._CapacityRequirementItem` |
| `CapReq._CapacityRequirementConf` | `CapReq._CapacityRequirementConf` |
| `CapReq._Capacity` | `CapReq._Capacity` |
| `CapReq._StatusObject` | `CapReq._StatusObject` |
| `CapReq._UnitOfMeasure` | `CapReq._UnitOfMeasure` |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_ProductionOrderType` | `I_ProductionOrderType` | [1..1] |
| `_Plant` | `I_Plant` | [1..1] |
| `_WorkCenterType` | `I_WorkCenterType` | [0..1] |
| `_WorkCenter` | `I_WorkCenter` | [0..1] |
| `_ProductionOrderHeader` | `I_ProductionOrder` | [1..1] |
| `_ProductionOrderSequence` | `I_ProductionOrderSequence` | [1..1] |

## Source Code

```abap
@AccessControl.authorizationCheck: #MANDATORY
@AccessControl.personalData.blocking: #NOT_REQUIRED
@AccessControl.privilegedAssociations: ['_StatusObject']
@Analytics.dataExtraction.enabled: true
@Analytics.technicalName: 'IPRODORDOPCAP'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.modelingPattern: #NONE
@ObjectModel.supportedCapabilities: [#CDS_MODELING_DATA_SOURCE, #EXTRACTION_DATA_SOURCE, #SQL_DATA_SOURCE]
@ObjectModel.sapObjectNodeType.name: 'ProdnOrderOperationCapacity'
@ObjectModel.semanticKey: ['CapacityRequirement', 'CapacityRequirementItem', 'CapacityRqmtItemCapacity']
@ObjectModel.usageType: {serviceQuality: #C, sizeCategory: #XL, dataClass: #TRANSACTIONAL}
@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API
@VDM.viewType: #COMPOSITE
@EndUserText.label: 'Individual Capacity of Production Order Operation'

define view entity I_ProdnOrderOperationCapacity
    as select from I_MfgOrderOperationBasic as OrderOperation
      inner to many join I_CapacityRequirementItemCap as CapReq on  OrderOperation.CapacityRequirement     = CapReq.CapacityRequirement
                                                                and OrderOperation.CapacityRequirementItem = CapReq.CapacityRequirementItem

  association [1..1] to I_ProductionOrderType         as _ProductionOrderType        on  $projection.ProductionOrderType = _ProductionOrderType.ProductionOrderType 
  association [1..1] to I_Plant                       as _Plant                      on  $projection.Plant = _Plant.Plant
  association [0..1] to I_WorkCenterType              as _WorkCenterType             on  $projection.WorkCenterTypeCode = _WorkCenterType.WorkCenterTypeCode
  association [0..1] to I_WorkCenter                  as _WorkCenter                 on  $projection.WorkCenterTypeCode   = _WorkCenter.WorkCenterTypeCode
                                                                                     and $projection.WorkCenterInternalID = _WorkCenter.WorkCenterInternalID
  -- SOT relations
  association [1..1] to I_ProductionOrder             as _ProductionOrderHeader      on  $projection.ProductionOrder = _ProductionOrderHeader.ProductionOrder
  association [1..1] to I_ProductionOrderSequence     as _ProductionOrderSequence    on  $projection.ProductionOrder         = _ProductionOrderSequence.ProductionOrder
                                                                                     and $projection.ProductionOrderSequence = _ProductionOrderSequence.ProductionOrderSequence
  association to parent I_ProductionOrderOperation_2  as _ProductionOrderOperation   on  $projection.OrderInternalID          = _ProductionOrderOperation.OrderInternalID
                                                                                     and $projection.OrderOperationInternalID = _ProductionOrderOperation.OrderOperationInternalID
{
      // Key
      @ObjectModel.foreignKey.association: '_CapacityRequirement'
  key CapReq.CapacityRequirement,
      @ObjectModel.foreignKey.association: '_CapacityRequirementItem'
  key CapReq.CapacityRequirementItem,
      -- representative key
  key CapReq.CapacityRqmtItemCapacity,

      // Order Data
      @ObjectModel.foreignKey.association: '_OrderInternalID'
      OrderOperation.OrderInternalBillOfOperations as OrderInternalID,
      OrderOperation.OrderIntBillOfOperationsItem  as OrderOperationInternalID,
      @Consumption.hidden: true
      OrderOperation.ManufacturingOrderCategory,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductionOrderTypeStdVH', element: 'ProductionOrderType' } } ]
      @ObjectModel.foreignKey.association: '_ProductionOrderType'
      cast(OrderOperation.ManufacturingOrderType as vdm_prodnordertype preserving type)             as ProductionOrderType,
 
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductionOrderStdVH', element: 'ProductionOrder' } } ]
      @ObjectModel.foreignKey.association: '_ProductionOrderHeader'
      cast(OrderOperation.ManufacturingOrder as vdm_manufacturingorder preserving type)             as ProductionOrder,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductionOrderSequenceVH', element: 'ProductionOrderSequence' } } ]      
      cast(OrderOperation.ManufacturingOrderSequence as vdm_prodnordersequence preserving type)     as ProductionOrderSequence,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductionOrderOperationVH', element: 'ProductionOrderOperation' } } ]
      cast(OrderOperation.ManufacturingOrderOperation_2 as vdm_prodnorderoperation preserving type) as ProductionOrderOperation,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_PlantStdVH', element: 'Plant' } } ]
      @ObjectModel.foreignKey.association: '_Plant'
      OrderOperation.ProductionPlant               as Plant,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_WorkCenterStdVH', element: 'WorkCenterInternalID' } } ]
      @ObjectModel.foreignKey.association: '_WorkCenter'
      OrderOperation.WorkCenterInternalID,
      @ObjectModel.foreignKey.association: '_WorkCenterType'
      OrderOperation.WorkCenterTypeCode_2          as WorkCenterTypeCode,
      
      // Assignments
      @ObjectModel.foreignKey.association: '_CapacityRequirementConf'    
      CapReq.CapacityRqmtConfirmation,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_CapacityStdVH', element: 'CapacityInternalID' } } ]
      @ObjectModel.foreignKey.association: '_Capacity'
      CapReq.CapacityInternalID,
      CapReq.CapacityRequirementDistrKey,

      // Capacity Durations
      @ObjectModel.foreignKey.association: '_UnitOfMeasure'
      CapReq.CapacityRequirementUnit,
      CapReq.ScheduledCapReqOpSegSetupDurn,
      CapReq.RemainingCapReqOpSegSetupDurn,
      CapReq.ScheduledCapReqOpSegProcgDurn,
      CapReq.RemainingCapReqOpSegProcgDurn,
      CapReq.ScheduledCapReqOpSegTrdwnDurn,
      CapReq.RemainingCapReqOpSegTrdwnDurn,

      // Dates and Times
      CapReq.OperationEarliestStartDate,
      CapReq.OperationEarliestStartTime,
      CapReq.OperationEarliestEndDate,
      CapReq.OperationEarliestEndTime,
      CapReq.OperationLatestStartDate,
      CapReq.OperationLatestStartTime,
      CapReq.OperationLatestEndDate,
      CapReq.OperationLatestEndTime,
      CapReq.ActualStartDate,
      CapReq.ActualStartTime,
      CapReq.ActualEndDate,
      CapReq.ActualEndTime,

      // Time Events
      CapReq.ErlstSchedldProcStrtDte,
      CapReq.ErlstSchedldProcStrtTime,
      CapReq.ErlstSchedldTrdwnStrtDte,
      CapReq.ErlstSchedldTrdwnStrtTime,
      CapReq.LtstSchedldProcStrtDte,
      CapReq.LtstSchedldProcStrtTime,
      CapReq.LtstSchedldTrdwnStrtDte,
      CapReq.LtstSchedldTrdwnStrtTime,

      // Split data
      CapReq.ActualNumberOfSplits,
      CapReq.CapacityRequirementSplit,
      @ObjectModel.foreignKey.association: '_StatusObject'
      CapReq.ObjectInternalID,

      // Order Data
      _ProductionOrderHeader,
      _ProductionOrderType,
      _ProductionOrderSequence,
      _ProductionOrderOperation,
      _Plant,
      _WorkCenter,
      _WorkCenterType,
      
      // Associations
      OrderOperation._OrderInternalID,
      CapReq._CapacityRequirement,
      CapReq._CapacityRequirementItem,
      CapReq._CapacityRequirementConf,
      CapReq._Capacity,
      CapReq._StatusObject,
      CapReq._UnitOfMeasure
}
where
  OrderOperation.ManufacturingOrderCategory = '10' and    // Production Order
  OrderOperation.SuperiorOperationInternalID = '00000000' // no sub-operations
```
