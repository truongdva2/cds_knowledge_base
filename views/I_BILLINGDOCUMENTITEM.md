---
name: I_BILLINGDOCUMENTITEM
description: Billing DocumentUMENTITEM
app_component: SD-BIL-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - SD
  - SD-BIL
  - interface-view
  - billing-document
  - billing
  - document
  - item-level
  - component:SD-BIL-2CL
  - lob:Sales & Distribution
  - bo:BillingDocument
---
# I_BILLINGDOCUMENTITEM

**Billing DocumentUMENTITEM**

| Property | Value |
|---|---|
| App Component | `SD-BIL-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `releaseState: #DEPRECATED, successor: '_ProductText' }` | `releaseState: #DEPRECATED, successor: '_ProductText' }` |
| `_MaterialText` | *Association* |
| `_ProductText` | *Association* |
| `_PricingReferenceMaterialText` | *Association* |
| `_MainItemPricingRefMatlText` | *Association* |
| `_OriginallyRequestedMatlText` | *Association* |
| `_SoldProductText` | *Association* |
| `_SDDocumentCategory` | *Association* |
| `_BillingDocumentCategory` | *Association* |
| `_BillingDocumentType` | *Association* |
| `_SalesOrganization` | *Association* |
| `_DistributionChannel` | *Association* |
| `_CustomerPriceGroup` | *Association* |
| `_CustomerGroup` | *Association* |
| `_Country` | *Association* |
| `_Region` | *Association* |
| `_CityCode` | *Association* |
| `_SalesDistrict` | *Association* |
| `_OverallSDProcessStatus` | *Association* |
| `_OverallBillingStatus` | *Association* |
| `_SoldToParty` | *Association* |
| `_PayerParty` | *Association* |
| `_CompanyCode` | *Association* |
| `_County` | *Association* |
| `_CancelledBillingDocument` | *Association* |
| `_ShipToParty` | *Association* |
| `_BillToParty` | *Association* |
| `_SalesEmployee` | *Association* |
| `_ResponsibleEmployee` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_BillingDocument` | `I_BillingDocument` | [1..1] |
| `_Partner` | `I_BillingDocItemPartner` | [0..*] |
| `_PricingElement` | `I_BillingDocumentItemPrcgElmnt` | [0..*] |
| `_HigherLevelItem` | `I_BillingDocumentItem` | [0..1] |
| `_HigherLvlItmOfBatSpltItm` | `I_BillingDocumentItem` | [0..1] |
| `_CancelledBillingDocument` | `I_BillingDocument` | [0..1] |
| `_Extension` | `E_BillingDocumentItem` | [1] |

## Source Code

```abap
@ClientHandling.algorithm: #SESSION_VARIABLE
@ObjectModel.usageType.dataClass: #TRANSACTIONAL
@ObjectModel.usageType.serviceQuality: #A
@ObjectModel.usageType.sizeCategory: #XL
@ObjectModel.representativeKey: 'BillingDocumentItem'
@ObjectModel.sapObjectNodeType.name: 'BillingDocumentItem'
@ObjectModel.modelingPattern:         #ANALYTICAL_DIMENSION
@ObjectModel.supportedCapabilities: [ #ANALYTICAL_DIMENSION,
                                      #CDS_MODELING_DATA_SOURCE,
                                      #CDS_MODELING_ASSOCIATION_TARGET,
                                      #SQL_DATA_SOURCE,
                                      #EXTRACTION_DATA_SOURCE ]
@EndUserText.label: 'Billing Document Item'
@Analytics.dataCategory: #DIMENSION
@Analytics: {
    dataExtraction: {
        enabled: true,
        delta.changeDataCapture: {
            mapping:[
                {
                    table: 'vbrp', role: #MAIN,
                    viewElement: ['BillingDocument', 'BillingDocumentItem'],
                    tableElement: ['vbeln', 'posnr']
                }

            ]
        }
    }
 }
@VDM.viewType: #BASIC
@AccessControl: {
  authorizationCheck: #CHECK,
  personalData.blocking: #('TRANSACTIONAL_DATA')
  //privilegedAssociations: [ '_CreatedByUser' ]
}
@AbapCatalog: {
  sqlViewName: 'ISDBILLGDOCITEM',
  compiler.compareFilter: true,
  preserveKey: true
}

//--[ GENERATED:012:GlBfhyJl7kY4uNsXH4jYtm
@AccessControl.privilegedAssociations: ['_BusinessAreaText','_MaterialGroupText', '_MainItemPricingRefMatlText', '_PricingReferenceMaterialText', '_OriginallyRequestedMatlText', '_MaterialText', '_ProductText', '_ShippingPointText','_WBSElementText','_CreatedByUser']
// ]--GENERATED

@Metadata.allowExtensions: true
@Metadata.ignorePropagatedAnnotations: true

define view I_BillingDocumentItem
  as select from I_BillingDocumentItemBasic

  association [1..1] to I_BillingDocument              as _BillingDocument
    on $projection.BillingDocument = _BillingDocument.BillingDocument

  association [0..*] to I_BillingDocItemPartner        as _Partner
    on  $projection.BillingDocument     = _Partner.BillingDocument
    and $projection.BillingDocumentItem = _Partner.BillingDocumentItem

  association [0..*] to I_BillingDocumentItemPrcgElmnt as _PricingElement
    on  $projection.BillingDocument     = _PricingElement.BillingDocument
    and $projection.BillingDocumentItem = _PricingElement.BillingDocumentItem

  association [0..1] to I_BillingDocumentItem          as _HigherLevelItem
    on  $projection.BillingDocument = _HigherLevelItem.BillingDocument
    and $projection.HigherLevelItem = _HigherLevelItem.BillingDocumentItem

  association [0..1] to I_BillingDocumentItem          as _HigherLvlItmOfBatSpltItm
    on  $projection.BillingDocument          = _HigherLvlItmOfBatSpltItm.BillingDocument
    and $projection.HigherLvlItmOfBatSpltItm = _HigherLvlItmOfBatSpltItm.BillingDocumentItem

  association [0..1] to I_BillingDocument              as _CancelledBillingDocument
    on $projection.CancelledBillingDocument = _CancelledBillingDocument.BillingDocument

  // Extension Association
  association [1] to    E_BillingDocumentItem          as _Extension
    on  $projection.BillingDocument     = _Extension.BillingDocument
    and $projection.BillingDocumentItem = _Extension.BillingDocumentItem

{
      // Key
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BillingDocumentBasicStdVH', element: 'BillingDocument' } } ]
      @ObjectModel.foreignKey.association: '_BillingDocument'
  key BillingDocument,

      @ObjectModel.text.element: [ 'BillingDocumentItemText' ]
  key BillingDocumentItem,

      // Category
      @ObjectModel.foreignKey.association: '_SalesDocumentItemCategory'
      SalesDocumentItemCategory,

      @ObjectModel.foreignKey.association: '_SalesDocumentItemType'
      SalesDocumentItemType,

      ReturnItemProcessingType,

      // Admin
      @Semantics.user.createdBy: true
      CreatedByUser,

      @Semantics.systemDate.createdAt: true
      CreationDate,

      @Semantics.systemTime.createdAt
      CreationTime,

      @ObjectModel.foreignKey.association: '_ReferenceLogicalSystem'
      ReferenceLogicalSystem,

      // Organization
      @ObjectModel.foreignKey.association: '_OrganizationDivision'
      OrganizationDivision,

      @ObjectModel.foreignKey.association: '_Division'
      Division,

      @ObjectModel.foreignKey.association: '_SalesOffice'
      SalesOffice,

      // Product
      @API.element: { releaseState: #DEPRECATED, successor: 'Product' }
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_MaterialStdVH', element: 'Material' } } ]
      @ObjectModel.foreignKey.association: '_Material'
      @ObjectModel.text.association: '_MaterialText'
      Material,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductStdVH', element: 'Product' } } ]
      @ObjectModel.foreignKey.association: '_Product'
      @ObjectModel.text.association: '_ProductText'
      Product,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_MaterialStdVH', element: 'Material' } } ]
      @ObjectModel.foreignKey.association: '_OriginallyRequestedMaterial'
      @ObjectModel.text.association: '_OriginallyRequestedMatlText'
      OriginallyRequestedMaterial,

      InternationalArticleNumber,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_MaterialStdVH', element: 'Material' } } ]
      @ObjectModel.foreignKey.association: '_PricingReferenceMaterial'
      @ObjectModel.text.association: '_PricingReferenceMaterialText'
      PricingReferenceMaterial,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BatchStdVH', element: 'Batch' },
                                            additionalBinding: [ { localElement: 'Plant',    element: 'Plant'    },
                                                                 { localElement: 'Material', element: 'Material' } ] } ]
      @ObjectModel.foreignKey.association: '_Batch'
      Batch,

      @ObjectModel.foreignKey.association: '_ProductHierarchyNode'
      ProductHierarchyNode,

      @API.element: { releaseState: #DEPRECATED, successor: 'ProductGroup' }
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_MaterialGroup', element: 'MaterialGroup' } } ]
      @ObjectModel.foreignKey.association: '_MaterialGroup'
      @ObjectModel.text.association: '_MaterialGroupText'
      MaterialGroup,

      @ObjectModel.foreignKey.association: '_ProductGroup'
      ProductGroup,

      @ObjectModel.foreignKey.association: '_AdditionalMaterialGroup1'
      AdditionalMaterialGroup1,

      @ObjectModel.foreignKey.association: '_AdditionalMaterialGroup2'
      AdditionalMaterialGroup2,

      @ObjectModel.foreignKey.association: '_AdditionalMaterialGroup3'
      AdditionalMaterialGroup3,

      @ObjectModel.foreignKey.association: '_AdditionalMaterialGroup4'
      AdditionalMaterialGroup4,

      @ObjectModel.foreignKey.association: '_AdditionalMaterialGroup5'
      AdditionalMaterialGroup5,

      ProductConfiguration,

      @ObjectModel.foreignKey.association: '_MaterialCommissionGroup'
      MaterialCommissionGroup,

      @ObjectModel.foreignKey.association: '_Plant'
      Plant,

      @ObjectModel.foreignKey.association: '_StorageLocation'
      StorageLocation,

      ReplacementPartType,
      MaterialGroupHierarchy1,
      MaterialGroupHierarchy2,

      @ObjectModel.foreignKey.association: '_PlantRegion'
      PlantRegion,

      PlantCounty,
      PlantCity,

      @ObjectModel.foreignKey.association: '_TransitPlant'
      TransitPlant,

      @ObjectModel.foreignKey.association: '_ValueChainCategory'
      ValueChainCategory,

      // ValueChainInstanceUUID,
      BOMExplosion,
      MaterialDeterminationType,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductStdVH', element: 'Product' } } ]
      @ObjectModel.foreignKey.association: '_SoldProduct'
      @ObjectModel.text.association: '_SoldProductText'
      SoldProduct,

      // Billing
      @Semantics.text: true
      BillingDocumentItemText,

      ServicesRenderedDate,

      @Semantics.quantity.unitOfMeasure: 'BillingQuantityUnit'
      BillingQuantity,

      @ObjectModel.foreignKey.association: '_BillingQuantityUnit'
      @Semantics.unitOfMeasure: true
      BillingQuantityUnit,

      @Semantics.quantity.unitOfMeasure: 'BaseUnit'
      BillingQuantityInBaseUnit,

      @ObjectModel.foreignKey.association: '_BaseUnit'
      @Semantics.unitOfMeasure: true
      BaseUnit,

      @Semantics.quantity.unitOfMeasure: 'BaseUnit'
      MRPRequiredQuantityInBaseUnit,

      BillingToBaseQuantityDnmntr,
      BillingToBaseQuantityNmrtr,

      @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
      ItemGrossWeight,

      @Semantics.quantity.unitOfMeasure: 'ItemWeightUnit'
      ItemNetWeight,

      @ObjectModel.foreignKey.association: '_ItemWeightUnit'
      @Semantics.unitOfMeasure: true
      ItemWeightUnit,

      @Semantics.quantity.unitOfMeasure: 'ItemVolumeUnit'
      ItemVolume,

      @ObjectModel.foreignKey.association: '_ItemVolumeUnit'
      @Semantics.unitOfMeasure: true
      ItemVolumeUnit,

      @ObjectModel.foreignKey.association: '_BillToPartyCountry'
      BillToPartyCountry,

      @ObjectModel.foreignKey.association: '_BillToPartyRegion'
      BillToPartyRegion,

      @ObjectModel.foreignKey.association: '_BillingPlanRule'
      BillingPlanRule,

      BillingPlan,
      BillingPlanItem,

      @ObjectModel.foreignKey.association: '_DownPaymentProcessingVariant'
      DownPaymentProcessingVariant,

      // Pricing
      @Semantics.amount.currencyCode: 'TransactionCurrency'
      NetAmount,

      @ObjectModel.foreignKey.association: '_TransactionCurrency'
      @Semantics.currencyCode: true
      TransactionCurrency,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      GrossAmount,

      PricingDate,
      PriceDetnExchangeRate,

      @Semantics.quantity.unitOfMeasure: 'BaseUnit'
      PricingScaleQuantityInBaseUnit,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      TaxAmount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      CostAmount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal1Amount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal2Amount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal3Amount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal4Amount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal5Amount,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      Subtotal6Amount,

      @ObjectModel.foreignKey.association: '_StatisticalValueControl'
      StatisticalValueControl,

      CashDiscountIsDeductible,
      CustomerConditionGroup1,
      CustomerConditionGroup2,
      CustomerConditionGroup3,
      CustomerConditionGroup4,
      CustomerConditionGroup5,
      ManualPriceChangeType,

      @ObjectModel.foreignKey.association: '_MaterialPricingGroup'
      MaterialPricingGroup,

      StatisticsExchangeRate,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_MaterialStdVH', element: 'Material' } } ]
      @ObjectModel.foreignKey.association: '_MainItemPricingRefMaterial'
      @ObjectModel.text.association: '_MainItemPricingRefMatlText'
      MainItemPricingRefMaterial,

      @ObjectModel.foreignKey.association: '_MainItemMaterialPricingGroup'
      MainItemMaterialPricingGroup,

      @ObjectModel.foreignKey.association: '_TimeSheetOvertimeCat'
      TimeSheetOvertimeCategory,

      @ObjectModel.foreignKey.association: '_PricingRelevance'
      PricingRelevance,

      // Tax
      @ObjectModel.foreignKey.association: '_DepartureCountry'
      DepartureCountry,

      TaxJurisdiction,
      ProductTaxClassification1,
      ProductTaxClassification2,
      ProductTaxClassification3,
      ProductTaxClassification4,
      ProductTaxClassification5,
      ProductTaxClassification6,
      ProductTaxClassification7,
      ProductTaxClassification8,
      ProductTaxClassification9,
      ZeroVATRsn,
      TaxCode,
      TaxRateValidityStartDate,

      // Foreign trade
      @ObjectModel.foreignKey.association: '_CountryOfOrigin'
      CountryOfOrigin,

      @ObjectModel.foreignKey.association: '_RegionOfOrigin'
      RegionOfOrigin,

      CommodityCode,

      // Payment
      @Semantics.amount.currencyCode: 'TransactionCurrency'
      EligibleAmountForCashDiscount,

      // Accounting
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BusinessAreaStdVH', element: 'BusinessArea' } } ]
      @ObjectModel.foreignKey.association: '_BusinessArea'
      @ObjectModel.text.association: '_BusinessAreaText'
      BusinessArea,

      @ObjectModel.foreignKey.association: '_ProfitCenter'
      ProfitCenter,

      OrderID,

      @API.element: { releaseState: #DEPRECATED, successor: 'WBSElementInternalID' }
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_WBSElementStdVH', element: 'WBSElement' } } ]
      @ObjectModel.foreignKey.association: '_WBSElement'
      @ObjectModel.text.association: '_WBSElementText'
      WBSElement,

      @ObjectModel.foreignKey.association: '_WBSElementBasicData'
      WBSElementInternalID,

      @ObjectModel.foreignKey.association: '_ProviderContract'
      ProviderContract,

      @ObjectModel.foreignKey.association: '_ProviderContractItem'
      ProviderContractItem,

      @API.element: { releaseState: #DEPRECATED, successor: 'BillingPeriodOfPerfStartDate' }
      BillingPerformancePeriodStrDte,

      BillingPeriodOfPerfStartDate,

      @API.element: { releaseState: #DEPRECATED, successor: 'BillingPeriodOfPerfEndDate' }
      BillingPerformancePeriodEndDte,

      BillingPeriodOfPerfEndDate,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ControllingAreaStdVH', element: 'ControllingArea' } } ]
      @ObjectModel.foreignKey.association: '_ControllingArea'
      ControllingArea,

      @API.element: { releaseState: #DECOMMISSIONED, successor: 'ProfitabilitySegment_2' }
      cast(case when ProfitabilitySegment_2 > '9999999999' then '9999999999' else lpad(ProfitabilitySegment_2, 10, '0') end as rkeobjnr_numc)
                                                                                                                        as ProfitabilitySegment,

      ProfitabilitySegment_2,

      @ObjectModel.foreignKey.association: '_CostCenter_2'
      CostCenter,

      OriginSDDocument,
      OriginSDDocumentItem,
      PriceDetnExchangeRateDate,

      @ObjectModel.foreignKey.association: '_MatlAccountAssignmentGroup'
      MatlAccountAssignmentGroup,

      // Reference
      ReferenceSDDocument,
      ReferenceSDDocumentItem,

      @ObjectModel.foreignKey.association: '_ReferenceSDDocumentCategory'
      ReferenceSDDocumentCategory,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_SalesDocumentStdVH', element: 'SalesDocument' } } ]
      @ObjectModel.foreignKey.association: '_SalesDocument'
      SalesDocument,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_SalesDocumentItemStdVH', element: 'SalesDocumentItem' },
                                            additionalBinding: [ { localElement: 'SalesDocument',
                                                                   element: 'SalesDocument' } ] } ]
      @ObjectModel.foreignKey.association: '_SalesDocumentItem'
      SalesDocumentItem,

      @ObjectModel.foreignKey.association: '_SalesSDDocumentCategory'
      SalesSDDocumentCategory,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BillingDocumentItemStdVH', element: 'BillingDocumentItem' },
                                            additionalBinding: [ { localElement: 'BillingDocument',
                                                                   element: 'BillingDocument' } ] } ]
      @ObjectModel.foreignKey.association: '_HigherLevelItem'
      HigherLevelItem,

      @ObjectModel.foreignKey.association: '_HigherLvlItmOfBatSpltItm'
      HigherLvlItmOfBatSpltItm,

      BillingDocumentItemInPartSgmt,
      ExternalReferenceDocument,

      @API.element: { releaseState: #DEPRECATED, successor: 'BillingDocExtReferenceDocItem' }
      ExternalReferenceDocumentItem,

      BillingDocExtReferenceDocItem,

      @ObjectModel.foreignKey.association: '_PrelimBillingDocument'
      PrelimBillingDocument,

      @ObjectModel.foreignKey.association: '_PrelimBillingDocumentItem'
      PrelimBillingDocumentItem,

      // Sales
      @ObjectModel.foreignKey.association: '_SalesGroup'
      SalesGroup,

      @ObjectModel.foreignKey.association: '_AdditionalCustomerGroup1'
      AdditionalCustomerGroup1,

      @ObjectModel.foreignKey.association: '_AdditionalCustomerGroup2'
      AdditionalCustomerGroup2,

      @ObjectModel.foreignKey.association: '_AdditionalCustomerGroup3'
      AdditionalCustomerGroup3,

      @ObjectModel.foreignKey.association: '_AdditionalCustomerGroup4'
      AdditionalCustomerGroup4,

      @ObjectModel.foreignKey.association: '_AdditionalCustomerGroup5'
      AdditionalCustomerGroup5,

      @ObjectModel.foreignKey.association: '_SDDocumentReason'
      SDDocumentReason,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_RetailPromotionStdVH', element: 'RetailPromotion' } } ]
      @ObjectModel.foreignKey.association: '_RetailPromotion'
      RetailPromotion,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      RebateBasisAmount,

      VolumeRebateGroup,
      ItemIsRelevantForCredit,

      @Semantics.amount.currencyCode: 'TransactionCurrency'
      CreditRelatedPrice,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_SalesDealStdVH', element: 'SalesDeal' } } ]
      @ObjectModel.foreignKey.association: '_SalesDeal'
      SalesDeal,

      SalesPromotion,

      @ObjectModel.foreignKey.association: '_SalesOrderSalesDistrict'
      SalesOrderSalesDistrict,

      @ObjectModel.foreignKey.association: '_SalesOrderCustomerGroup'
      SalesOrderCustomerGroup,

      @ObjectModel.foreignKey.association: '_SalesOrderCustomerPriceGroup'
      SalesOrderCustomerPriceGroup,

      @ObjectModel.foreignKey.association: '_SalesOrderPriceListType'
      SalesOrderPriceListType,

      @ObjectModel.foreignKey.association: '_SalesOrderSalesOrganization'
      SalesOrderSalesOrganization,

      @ObjectModel.foreignKey.association: '_SalesOrderDistributionChannel'
      SalesOrderDistributionChannel,

      SalesDocIsCreatedFromReference,

      // Shipping
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ShippingPointStdVH', element: 'ShippingPoint' } } ]
      @ObjectModel.foreignKey.association: '_ShippingPoint'
      @ObjectModel.text.association: '_ShippingPointText'
      ShippingPoint,

      // CRM
      @ObjectModel.foreignKey.association: '_ServiceDocumentType'
      ServiceDocumentType,

      ServiceDocument,
      ServiceDocumentItem,

      // Solution Order
      BusinessSolutionOrder,
      BusinessSolutionOrderItem,

      // Other fields
      HigherLevelItemUsage,
      BillingDocumentIsTemporary,

      // Added Analytics fields
      @ObjectModel.foreignKey.association: '_SDDocumentCategory'
      SDDocumentCategory,

      @ObjectModel.foreignKey.association: '_BillingDocumentType'
      BillingDocumentType,

      @ObjectModel.foreignKey.association: '_SalesOrganization'
      SalesOrganization,

      @ObjectModel.foreignKey.association: '_DistributionChannel'
      DistributionChannel,

      @ObjectModel.foreignKey.association: '_CustomerPriceGroup'
      CustomerPriceGroup,

      @ObjectModel.foreignKey.association: '_CustomerGroup'
      CustomerGroup,

      @ObjectModel.foreignKey.association: '_Country'
      Country,

      @ObjectModel.foreignKey.association: '_Region'
      Region,

      @ObjectModel.foreignKey.association: '_CityCode'
      CityCode,

      @ObjectModel.foreignKey.association: '_SalesDistrict'
      SalesDistrict,

      @ObjectModel.foreignKey.association: '_OverallSDProcessStatus'
      OverallSDProcessStatus,

      @ObjectModel.foreignKey.association: '_OverallBillingStatus'
      OverallBillingStatus,

      // sales
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Customer_VH', element: 'Customer' } } ]
      @ObjectModel.foreignKey.association: '_SoldToParty'
      SoldToParty,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Customer_VH', element: 'Customer' } } ]
      @ObjectModel.foreignKey.association: '_PayerParty'
      PayerParty,

      BillingDocumentDate,

      @ObjectModel.foreignKey.association: '_CompanyCode'
      CompanyCode,

      @ObjectModel.foreignKey.association: '_County'
      County,

      CustomerRebateAgreement,

      @ObjectModel.foreignKey.association: '_BillingDocumentCategory'
      BillingDocumentCategory,

      PricingDocument,

      @ObjectModel.foreignKey.association: '_CancelledBillingDocument'
      CancelledBillingDocument,

      // ship-to
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Customer_VH', element: 'Customer' } } ]
      @ObjectModel.foreignKey.association: '_ShipToParty'
      ShipToParty,

      // bill-to
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Customer_VH', element: 'Customer' } } ]
      @ObjectModel.foreignKey.association: '_BillToParty'
      BillToParty,

      @ObjectModel.foreignKey.association: '_SalesEmployee'
      SalesEmployee,

      @ObjectModel.foreignKey.association: '_ResponsibleEmployee'
      ResponsibleEmployee,

      // GTS 
      @ObjectModel.foreignKey.association: '_CustomsPreference'
      CustomsPreference,

      // Note: The field contains code values. Code list view is not available.
      TrdPrefStmntExternalOriginCode,

      @ObjectModel.foreignKey.association: '_TrdPrefStmntIntOriginCode'
      TrdPrefStmntInternalOriginCode,

      @Semantics.booleanIndicator: true
      PricingIsIncomplete,
      
      // association
      @ObjectModel.association.type: [ #TO_COMPOSITION_PARENT, #TO_COMPOSITION_ROOT ]
      _BillingDocument,

      @ObjectModel.association.type: [ #TO_COMPOSITION_CHILD ]
      _Partner,

      @ObjectModel.association.type: [ #TO_COMPOSITION_CHILD ]
      _PricingElement,

      _SalesDocumentItemCategory,
      _SalesDocumentItemType,
      _CreatedByUser,
      _ReferenceLogicalSystem,
      _OrganizationDivision,
      _Division,
      _SalesOffice,

      @API.element: { releaseState: #DEPRECATED, successor: '_Product' }
      _Material,

      _Product,
      _OriginallyRequestedMaterial,
      _PricingReferenceMaterial,
      _Batch,
      _ProductHierarchyNode,

      @API.element: { releaseState: #DEPRECATED, successor: '_ProductGroup' }
      _MaterialGroup,

      _ProductGroup,
      _AdditionalMaterialGroup1,
      _AdditionalMaterialGroup2,
      _AdditionalMaterialGroup3,
      _AdditionalMaterialGroup4,
      _AdditionalMaterialGroup5,
      _MaterialCommissionGroup,
      _Plant,
      _SoldProduct,
      _StorageLocation,
      _PlantRegion,
      _TransitPlant,
      _ValueChainCategory,
      _BillingQuantityUnit,
      _BaseUnit,
      _ItemWeightUnit,
      _ItemVolumeUnit,
      _BillToPartyCountry,
      _BillToPartyRegion,
      _BillingPlanRule,
      _DownPaymentProcessingVariant,
      _TransactionCurrency,
      _StatisticalValueControl,
      _MaterialPricingGroup,
      _MainItemPricingRefMaterial,
      _MainItemMaterialPricingGroup,
      _TimeSheetOvertimeCat,
      _PricingRelevance,
      _DepartureCountry,
      _CountryOfOrigin,
      _RegionOfOrigin,
      _BusinessArea,
      _ProfitCenter,

      @API.element: { releaseState: #DEPRECATED, successor: '_WBSElementBasicData' }
      _WBSElement,

      _WBSElementBasicData,
      _ControllingArea,
      _MatlAccountAssignmentGroup,

      @API.element: { releaseState: #DEPRECATED, successor: '_CostCenter_2' }
      _CostCenter,

      _CostCenter_2,
      _ReferenceSalesDocumentItem,
      _ReferenceDeliveryDocumentItem,
      _ReferenceBillingDocItemBasic,
      _ReferenceSDDocumentCategory,
      _SalesDocument,
      _SalesDocumentItem,
      _SalesSDDocumentCategory,
      _HigherLevelItem,
      _HigherLvlItmOfBatSpltItm,
      _PrelimBillingDocument,
      _PrelimBillingDocumentItem,
      _SalesGroup,
      _AdditionalCustomerGroup1,
      _AdditionalCustomerGroup2,
      _AdditionalCustomerGroup3,
      _AdditionalCustomerGroup4,
      _AdditionalCustomerGroup5,
      _SDDocumentReason,
      _RetailPromotion,
      _SalesDeal,
      _SalesOrderSalesDistrict,
      _SalesOrderCustomerGroup,
      _SalesOrderCustomerPriceGroup,
      _SalesOrderPriceListType,
      _SalesOrderSalesOrganization,
      _SalesOrderDistributionChannel,
      _ShippingPoint,
      _ServiceDocumentType,
      _ProviderContract,
      _ProviderContractItem,
      _SolutionOrder,
      _SolutionOrderItem,
      _CustomsPreference,
      _TrdPrefStmntIntOriginCode,

      // --[ GENERATED:012:GlBfhyJl7kY4uNsXH4jYtm
      @Consumption.hidden: true
      _BusinessAreaText,

      @API.element: { releaseState: #DEPRECATED, successor: '_WBSElementBasicData' }
      @Consumption.hidden: true
      _WBSElementText,

      @Consumption.hidden: true
      _MaterialGroupText,

      @Consumption.hidden: true
      _ShippingPointText,

      @API.element: { releaseState: #DEPRECATED, successor: '_ProductText' }
      _MaterialText,

      @Consumption.hidden: true
      _ProductText,

      @Consumption.hidden: true
      _PricingReferenceMaterialText,

      @Consumption.hidden: true
      _MainItemPricingRefMatlText,

      @Consumption.hidden: true
      _OriginallyRequestedMatlText,

      @Consumption.hidden: true
      _SoldProductText,
      // ]--GENERATED

      // Analytics
      _SDDocumentCategory,
      _BillingDocumentCategory,
      _BillingDocumentType,
      _SalesOrganization,
      _DistributionChannel,
      _CustomerPriceGroup,
      _CustomerGroup,
      _Country,
      _Region,
      _CityCode,
      _SalesDistrict,
      _OverallSDProcessStatus,
      _OverallBillingStatus,
      _SoldToParty,
      _PayerParty,
      _CompanyCode,
      _County,
      _CancelledBillingDocument,
      _ShipToParty,
      _BillToParty,
      _SalesEmployee,
      _ResponsibleEmployee
}

where SDDocumentCategory = 'M' // Invoice
   or SDDocumentCategory = 'N' // Invoice Canceled
   or SDDocumentCategory = 'O' // Credit Memo
   or SDDocumentCategory = 'P' // Debit Memo
   or SDDocumentCategory = 'S' // Credit Memo Canceled
   or SDDocumentCategory = 'U' // Pro Forma Invoice
   or SDDocumentCategory = '5' // Intercompany Invoice
   or SDDocumentCategory = '6' // Intercompany Credit Memo
   or SDDocumentCategory = 'DPRQ' // Down Payment Request
   or SDDocumentCategory = 'DPCC' // Down Payment Cancellation
   or SDDocumentCategory = 'ICPF' // Intercompany Pro Forma Invoice
```
