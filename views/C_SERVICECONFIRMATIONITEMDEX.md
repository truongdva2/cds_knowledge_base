---
name: C_SERVICECONFIRMATIONITEMDEX
description: Serviceconfirmationitemdex
app_component: CRM-S4-ANA-BI-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - CRM
  - consumption-view
  - data-extraction
  - service
  - item-level
  - component:CRM-S4-ANA-BI-2CL
  - lob:Other
---
# C_SERVICECONFIRMATIONITEMDEX

**Serviceconfirmationitemdex**

| Property | Value |
|---|---|
| App Component | `CRM-S4-ANA-BI-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `currencyCode: 'TransactionCurrency'} }` | `currencyCode: 'TransactionCurrency'} }` |
| `ServiceOrderItem.ServiceDocItemGrossAmount` | `ServiceOrderItem.ServiceDocItemGrossAmount` |
| `/* Item Product */` | `/* Item Product */` |
| `ServiceOrderItem.ProductGroup` | `ServiceOrderItem.ProductGroup` |
| `SrvcMaterialPricingGroup` | `ServiceOrderItem.ProductGroup` |
| `ServiceOrderItem.ProductGroup1` | `ServiceOrderItem.ProductGroup1` |
| `ServiceOrderItem.ProductGroup2` | `ServiceOrderItem.ProductGroup2` |
| `ServiceOrderItem.ProductGroup3` | `ServiceOrderItem.ProductGroup3` |
| `ServiceOrderItem.ProductGroup4` | `ServiceOrderItem.ProductGroup4` |
| `ServiceOrderItem.ProductGroup5` | `ServiceOrderItem.ProductGroup5` |
| `ServiceOrderItem.ProductHierarchy` | `ServiceOrderItem.ProductHierarchy` |
| `/* Service Subject */` | `/* Service Subject */` |
| `/* Reference Object Details */` | `/* Reference Object Details */` |
| `ServiceOrdItemRefObj.ServiceRefObjectSequenceNumber` | `ServiceOrdItemRefObj.ServiceRefObjectSequenceNumber` |
| `ServiceOrdItemRefObj.ServiceReferenceObjectType` | `ServiceOrdItemRefObj.ServiceReferenceObjectType` |
| `ServiceOrdItemRefObj.ServiceReferenceObjectUUID` | `ServiceOrdItemRefObj.ServiceReferenceObjectUUID` |
| `ServiceOrdItemRefObj.ProductID` | `ServiceOrdItemRefObj.ProductID` |
| `ServiceOrdItemRefObj.SrvcRefObjIsMainObject` | `ServiceOrdItemRefObj.SrvcRefObjIsMainObject` |
| `ServiceOrdItemRefObj.ProductUUID` | `ServiceOrdItemRefObj.ProductUUID` |
| `ServiceOrdItemRefObj.SerialNumber` | `ServiceOrdItemRefObj.SerialNumber` |
| `ServiceOrdItemRefObj.Equipment` | `ServiceOrdItemRefObj.Equipment` |
| `ServiceOrdItemRefObj.FunctionalLocation` | `ServiceOrdItemRefObj.FunctionalLocation` |
| `ServiceDocumentPredecessor.ServiceDocumentRelationUUID` | `ServiceDocumentPredecessor.ServiceDocumentRelationUUID` |
| `ServiceDocumentRelationUUID_2` | `ServiceDocumentPredecessor.ServiceDocumentRelationUUID` |
| `ServiceDocumentPredecessor.ServiceDocRltnSequenceNumber` | `ServiceDocumentPredecessor.ServiceDocRltnSequenceNumber` |
| `ServiceDocRltnSequenceNumber_2` | `ServiceDocumentPredecessor.ServiceDocRltnSequenceNumber` |
| `SrvcOrdPredecessorUUID` | `ServiceDocumentPredecessor.ServiceDocumentPredecessorUUID` |
| `SrvcOrdPrdcssrBusObjType` | `ServiceDocumentPredecessor.ServiceDocPrdcssrBusObjType` |
| `ServiceDocumentItemPrdcssr.ServiceDocumentItmRelationUUID` | `ServiceDocumentItemPrdcssr.ServiceDocumentItmRelationUUID` |
| `SrvcDocItmRelationUUID_2` | `ServiceDocumentItemPrdcssr.ServiceDocumentItmRelationUUID` |
| `ServiceDocumentItemPrdcssr.ServiceDocItmRltnSqncNumber` | `ServiceDocumentItemPrdcssr.ServiceDocItmRltnSqncNumber` |
| `ServiceDocItmRltnSqncNumber_2` | `ServiceDocumentItemPrdcssr.ServiceDocItmRltnSqncNumber` |
| `SrvcOrdItmPrdcssrCharUUID` | `ServiceDocumentItemPrdcssr.ServiceDocItmPrdcssrCharUUID` |
| `SrvcOrdItmPrdcssrBusObjType` | `ServiceDocumentItemPrdcssr.ServiceDocItmPrdcssrBusObjType` |
| `SrvcOrdItmPrdcssrCharUUID_2` | `ServiceOrderItemPrdcssr.ServiceDocItmPrdcssrCharUUID` |
| `SrvcOrdItmPrdcssrBusObjType_2` | `ServiceOrderItemPrdcssr.ServiceDocItmPrdcssrBusObjType` |
| `/*  Associations  */` | `/*  Associations  */` |
| `/* Keys */` | `/* Keys */` |
| `ServiceOrderItem._ServiceObjType` | `ServiceOrderItem._ServiceObjType` |
| `ServiceOrderItem._ServiceDocument` | `ServiceOrderItem._ServiceDocument` |
| `/* Item - Product */` | `/* Item - Product */` |
| `ServiceOrderItem._BaseUnit_2` | `ServiceOrderItem._BaseUnit_2` |
| `ServiceOrderItem._SalesUnit` | `ServiceOrderItem._SalesUnit` |
| `/* Pricing Parameter Set */` | `/* Pricing Parameter Set */` |
| `ServiceOrderItem._TransactionCurrency` | `ServiceOrderItem._TransactionCurrency` |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_ServiceDocItemExt` | `E_ServiceDocumentItem` | [0..1] |
| `_ServiceDocumentExt` | `E_ServiceDocument` | [0..1] |

## Source Code

```abap
@EndUserText.label: 'Data Extraction for Service Confirmation'
@VDM: {
  viewType: #CONSUMPTION,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
}
@Analytics: {
    dataCategory: #FACT,
    dataExtraction: {
        enabled: true,
        delta.changeDataCapture: {
            mapping:[
                {
                   filter: [{operator: #EQ, tableElement: 'objtype_h', value: 'BUS2000117'}],
                    table: 'crms4d_serv_i', role: #MAIN,
                    viewElement: ['ServiceObjectType', 'ServiceDocument', 'ServiceDocumentItem'],
                    tableElement: ['objtype_h', 'object_id', 'number_int']
                },
                {
                    filter: [{operator: #EQ, tableElement: 'objtype_h', value: 'BUS2000117'}],
                    table: 'crms4d_serv_h', role: #LEFT_OUTER_TO_ONE_JOIN,
                    viewElement: ['ServiceObjectType', 'ServiceDocument'],
                    tableElement: ['objtype_h', 'object_id']
                },
                {
                    filter: [{operator: #EQ, tableElement: 'objtype_h', value: 'BUS2000117'}],
                    table: 'crms4d_refobj', role: #LEFT_OUTER_TO_ONE_JOIN,
                    viewElement: ['ServiceObjectType', 'ServiceDocument','ServiceDocumentItem','ServiceRefObjectSequenceNumber'],
                    tableElement: ['objtype_h', 'object_id','number_int','counter']
                }
//                {
//                    filter: [{operator: #EQ, tableElement: 'objtype_b_sel', value: 'BUS2000117'}],
//                    table: 'crmd_brelvonae', role: #LEFT_OUTER_TO_ONE_JOIN,
//                    viewElement: ['ServiceDocumentRelationUUID', 'ServiceDocRltnSequenceNumber'],
//                    tableElement: ['relationid', 'posno']
//                },
//                {
//                   filter: [{operator: #EQ, tableElement: 'objtype_b', value: 'BUS2000117'}],
//                    table: 'crmd_brelvonai', role: #LEFT_OUTER_TO_ONE_JOIN,
//                    viewElement: ['ServiceDocumentItmRelationUUID', 'ServiceDocItmRltnSqncNumber'],
//                    tableElement: ['relationid', 'posno']
//                }
            ]
        }
    }
 }
@Analytics.technicalName: 'CSRVCCONFITMDEX'
@AccessControl: {
  authorizationCheck: #CHECK,
  personalData.blocking: #('TRANSACTIONAL_DATA')
}
@ObjectModel: {
  representativeKey: 'ServiceDocumentItem',
   usageType: {
     dataClass:      #TRANSACTIONAL,
     serviceQuality: #D,
     sizeCategory:   #XL
   },
   supportedCapabilities: [#EXTRACTION_DATA_SOURCE],
   modelingPattern: #NONE
}
@ObjectModel.sapObjectNodeType.name: 'ServiceConfirmationItem'
@Metadata.ignorePropagatedAnnotations: true

define view entity C_ServiceConfirmationItemDEX
  as select from           I_ServiceDocumentItemEnhcd     as ServiceOrderItem
    left outer to one join I_ServiceDocumentEnhcd         as ServiceOrder               on  ServiceOrder.ServiceObjectType     = 'BUS2000117'
                                                                                        and ServiceOrderItem.ServiceObjectType = 'BUS2000117'
                                                                                        and ServiceOrderItem.ServiceDocument   = ServiceOrder.ServiceDocument

    left outer to one join I_ServiceDocumentRefObject     as ServiceOrdItemRefObj       on  ServiceOrdItemRefObj.ServiceObjectType      = 'BUS2000117'
                                                                                        and ServiceOrdItemRefObj.ServiceDocument        = ServiceOrderItem.ServiceDocument
                                                                                        and ServiceOrdItemRefObj.ServiceDocumentItem    = ServiceOrderItem.ServiceDocumentItem
                                                                                        and ServiceOrdItemRefObj.SrvcRefObjIsMainObject = 'X'

    left outer to one join I_ServiceDocumentItemPrdcssr_2 as ServiceDocumentItemPrdcssr on  ServiceOrderItem.ServiceDocumentItemCharUUID   = ServiceDocumentItemPrdcssr.ServiceDocumentItemCharUUID
                                                                                        and ServiceOrderItem.ServiceDocumentItemObjectType = ServiceDocumentItemPrdcssr.ServiceDocumentItemObjectType
                                                                                        and ServiceOrderItem.ServiceDocumentItemObjectType = 'BUS2000137'

    left outer to one join I_ServiceDocumentItemPrdcssr_2 as ServiceOrderItemPrdcssr    on  ServiceOrderItem.ServiceDocumentItemCharUUID          = ServiceOrderItemPrdcssr.ServiceDocumentItemCharUUID
                                                                                        and ServiceOrderItem.ServiceDocumentItemObjectType        = ServiceOrderItemPrdcssr.ServiceDocumentItemObjectType
                                                                                        and ServiceOrderItemPrdcssr.ServiceDocumentItemObjectType = 'BUS2000137'

    left outer to one join I_ServiceDocumentPredecessor   as ServiceDocumentPredecessor on  ServiceOrder.ServiceDocumentUUID                       = ServiceDocumentPredecessor.ServiceDocumentUUID
                                                                                        and ServiceOrder.ServiceObjectType                         = ServiceDocumentPredecessor.ServiceObjectType
                                                                                        and ServiceDocumentPredecessor.ServiceDocPrdcssrBusObjType = 'BUS2000116'

  association [0..1] to E_ServiceDocumentItem as _ServiceDocItemExt  on  $projection.ServiceObjectType   = _ServiceDocItemExt.ServiceCategory
                                                                     and $projection.ServiceDocument     = _ServiceDocItemExt.ServiceOrderItem
                                                                     and $projection.ServiceDocumentItem = _ServiceDocItemExt.ServiceLineExternalID

  association [0..1] to E_ServiceDocument     as _ServiceDocumentExt on  $projection.ServiceObjectType = _ServiceDocumentExt.ServiceCategory
                                                                     and $projection.ServiceDocument   = _ServiceDocumentExt.ServiceOrder

{
      /* Service Order Item Keys  */
      @ObjectModel.foreignKey.association: '_ServiceObjType'
  key ServiceOrderItem.ServiceObjectType,
      @ObjectModel.foreignKey.association: '_ServiceDocument'
  key ServiceOrderItem.ServiceDocument,
  key ServiceOrderItem.ServiceDocumentItem,

      /* Header Details */
      ServiceOrder.ServiceDocumentUUID,
      @Semantics.uuid
      ServiceOrder.ServiceDocumentCharUUID,
      ServiceOrder.ServiceDocumentDescription,
      @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
      ServiceOrder.ServiceDocNetAmount,
      ServiceOrder.PurchaseOrderByCustomer,
      ServiceOrder.ServiceDocumentStatus,
      @Semantics.booleanIndicator: true
      ServiceOrder.ServiceDocumentHasError,
      @Semantics.booleanIndicator: true
      ServiceOrder.ServiceDocIsReleasedForBilling,
      ServiceOrder.ServiceDocBillingStatus,
      @Semantics.booleanIndicator: true
      ServiceOrder.ServiceDocumentIsOpen,
      ServiceOrder.BusinessActivityCategory,
      ServiceOrder.ServiceDocChangedDateTime,
      ServiceOrder.ServiceDocCreationDateTime,
      //      ServiceOrder.ServiceDocumentUrgency,
      //      ServiceOrder.ServiceDocumentImpact,
      //      ServiceOrder.ServiceDocumentProblemCategory,
      //      ServiceOrder.RecommendedServicePriority,
      //      ServiceOrder.ServiceEscalationLevel,
      //      ServiceOrder.ServiceRisk,
      ServiceOrder.RequestedServiceStartDate,
      ServiceOrder.RequestedServiceEndDate,

      /* Item Details */
      ServiceOrderItem.ServiceDocumentItemPriority,
      ServiceOrderItem.ServiceDocumentType,
      ServiceOrderItem.ServiceDocumentItemUUID,
      //      ServiceOrderItem.ServiceDocumentItemCharUUID,
      ServiceOrderItem.ServiceDocumentItemObjectType,
      ServiceOrderItem.ParentServiceDocumentItemUUID,
      ServiceOrderItem.PostingDate,
      ServiceOrderItem.ProfitCenter,
      ServiceOrderItem.ControllingArea,
      ServiceOrderItem.ProfitCenterDeterminationDate,
      ServiceOrderItem.ServiceDocItemCategory,
      ServiceOrderItem.OriginallyRequestedProduct,
      ServiceOrderItem.Product,
      ServiceOrderItem.ActualServiceStartDateTime,
      ServiceOrderItem.ActualServiceEndDateTime,
      @DefaultAggregation: #SUM
      @Semantics: { quantity : {unitOfMeasure: 'ActualServiceDurationUnit'} }
      ServiceOrderItem.ActualServiceDuration,
      //      @ObjectModel.foreignKey.association: '_ActualServiceDurationUnit'
      //      @Semantics.unitOfMeasure:true
      ServiceOrderItem.ActualServiceDurationUnit,
      //      ServiceOrderItem.ServiceWorkDuration,
      //      ServiceOrderItem.ServiceWorkDurationUnit,
      ServiceOrderItem.TimeSheetOvertimeCategory,

      /* Item - Cumulated Value */
      @DefaultAggregation: #SUM
      @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
      ServiceOrderItem.InvoiceAmountInInvoiceDocCrcy,
      @Semantics: { quantity : {unitOfMeasure: 'ServiceDocItemQuantityUnit'} }
      ServiceOrderItem.SrvcDocItmRequestedQuantity,
      @Semantics: { quantity : {unitOfMeasure: 'ServiceDocItemQuantityUnit'} }
      ServiceOrderItem.InvoiceQuantity,
      @Semantics: { quantity : {unitOfMeasure: 'ServiceDocItemQuantityUnit'} }
      ServiceOrderItem.SrvcDocItmDeliveredQuantity,
      //      @Semantics: { quantity : {unitOfMeasure: 'ServiceDocItemQuantityUnit'} }
      //      ServiceOrderItem.SrvcDocItmReleasedQuantity,
      //      @DefaultAggregation: #SUM
      //      @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
      //      ServiceOrderItem.SrvcDocItmReldAmtInTransCrcy,

      /* Item - Schedule Line */
      //      @DefaultAggregation: #SUM
      //      @Semantics: { quantity : {unitOfMeasure: 'ServiceDocItemQuantityUnit'} }
      //      ServiceOrderItem.ServiceDocumentItemQuantity,

      /* Item - Product */
      ServiceOrderItem.SrvcDocItmOrdToBaseQtyNmrtr,
      ServiceOrderItem.SrvcDocItmOrdToBaseQtyDnmntr,
      ServiceOrderItem.OrderToBaseQuantityExponent,
      @ObjectModel.foreignKey.association: '_SalesUnit'
      ServiceOrderItem.ServiceDocItemQuantityUnit,
      @ObjectModel.foreignKey.association: '_BaseUnit_2'
      ServiceOrderItem.SrvcDocItemBaseQuantityUnit,

      /* Partner  */
      ServiceOrderItem.ServiceTeam,
      ServiceOrderItem.SoldToParty,
      ServiceOrderItem.SoldToPartyCountry,
      ServiceOrderItem.SoldToPartyRegion,
      ServiceOrderItem.ResponsibleEmployee,
      ServiceOrderItem.ContactPerson,

      /* Pricing Parameter Set */
      @ObjectModel.foreignKey.association: '_TransactionCurrency'
      ServiceOrderItem.TransactionCurrency,
      ServiceOrderItem.StatisticsCurrency,
      ServiceOrderItem.ExchangeRateType,
      ServiceOrderItem.ExchangeRateDate,
      ServiceOrderItem.AccountingExchangeRate,
      ServiceOrderItem.CustomerGroup,
      ServiceOrderItem.BillableControl,

      /* Billing Set */
      ServiceOrderItem.BillingBlockReason,

      /* Item - Sales Set */
      ServiceOrderItem.AdditionalCustomerGroup1,
      ServiceOrderItem.AdditionalCustomerGroup2,
      ServiceOrderItem.AdditionalCustomerGroup3,
      ServiceOrderItem.AdditionalCustomerGroup4,
      ServiceOrderItem.AdditionalCustomerGroup5,

      /* Organization Unit Set */
      ServiceOrderItem.SalesOrganizationOrgUnitID,
      ServiceOrderItem.SalesOfficeOrgUnitID,
      ServiceOrderItem.SalesGroupOrgUnitID,
      ServiceOrderItem.DistributionChannel,
      ServiceOrderItem.Division,
      ServiceOrderItem.ServiceOrganization,
      ServiceOrderItem.ResponsibleSalesOrganization,
      ServiceOrderItem.ResponsibleServiceOrganization,

      /* Organizational units with the S/4 SD codes */
      ServiceOrderItem.SalesOrganization,
      ServiceOrderItem.SalesOffice,
      ServiceOrderItem.SalesGroup,

      /* Item - Service */
      //      @DefaultAggregation: #SUM
      //      @Semantics: { quantity : {unitOfMeasure: 'ServiceDurationUnit'} }
      //      ServiceOrderItem.ServiceDuration,
      //      ServiceOrderItem.ServiceDurationUnit,

      /* Status */
      ServiceOrderItem.ServiceDocumentItemStatus,
      //      ServiceOrderItem.ServiceDocumentItemIsReleased,
      //      ServiceOrderItem.ServiceDocumentItemIsQuotation,
      @Semantics.booleanIndicator: true
      ServiceOrderItem.ServiceDocumentItemHasError,
      //      ServiceOrderItem.SrvcDocItmDeliveryStatus,
      //      ServiceOrderItem.SrvcDocItmGoodsIssueStatus,
      ServiceOrderItem.ServiceDocItemBillingStatus,
      @Semantics.booleanIndicator: true
      ServiceOrderItem.SrvcDocItmIsReleasedForBilling,
      @Semantics.booleanIndicator: true
      ServiceOrderItem.ServiceDocumentItemIsRejected,
      @Semantics.booleanIndicator: true
      ServiceOrderItem.ServiceDocumentItemIsOpen,
      //      ServiceOrderItem.QuotationIsAccepted,

      /* Item - Pricing */
      @DefaultAggregation: #SUM
      @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
      ServiceOrderItem.ServiceDocumentItemNetAmount,
      @DefaultAggregation: #SUM
      @Semantics: { amount : {currencyCode: 'TransactionCurrency'} }
      ServiceOrderItem.ServiceDocItemGrossAmount,

      /* Item Product */
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'SrvcMaterialPricingGroup'
      ServiceOrderItem.ProductGroup,
      ServiceOrderItem.ProductGroup                             as SrvcMaterialPricingGroup,
      ServiceOrderItem.ProductGroup1,
      ServiceOrderItem.ProductGroup2,
      ServiceOrderItem.ProductGroup3,
      ServiceOrderItem.ProductGroup4,
      ServiceOrderItem.ProductGroup5,
      ServiceOrderItem.ProductHierarchy,

      /* Service Subject */
      //      ServiceOrderItem.ServiceSchema1Category1UUID,
      //      ServiceOrderItem.ServiceSchema1Category2UUID,
      //      ServiceOrderItem.ServiceSchema1Category3UUID,
      //      ServiceOrderItem.ServiceSchema1Category4UUID,
      //      ServiceOrderItem.ServiceSchema1Category5UUID,
      //      ServiceOrderItem.ServiceSchema1Category6UUID,
      //      ServiceOrderItem.ServiceSchema1Category7UUID,
      //      ServiceOrderItem.ServiceSchema1Category8UUID,
      //      ServiceOrderItem.ServiceSchema1Category9UUID,
      //      ServiceOrderItem.ServiceSchema1Category10UUID,
      //      ServiceOrderItem.SrvcCatztnSchema1,
      //      ServiceOrderItem.SrvcCatztnSchemaCategory1,
      //
      //      ServiceOrderItem.ServiceSchema2Category1UUID,
      //      ServiceOrderItem.ServiceSchema2Category2UUID,
      //      ServiceOrderItem.ServiceSchema2Category3UUID,
      //      ServiceOrderItem.ServiceSchema2Category4UUID,
      //      ServiceOrderItem.ServiceSchema2Category5UUID,
      //      ServiceOrderItem.ServiceSchema2Category6UUID,
      //      ServiceOrderItem.ServiceSchema2Category7UUID,
      //      ServiceOrderItem.ServiceSchema2Category8UUID,
      //      ServiceOrderItem.ServiceSchema2Category9UUID,
      //      ServiceOrderItem.ServiceSchema2Category10UUID,
      //      ServiceOrderItem.SrvcCatztnSchema2,
      //      ServiceOrderItem.SrvcCatztnSchemaCategory2,

      /* Reference Object Details */
      ServiceOrdItemRefObj.ServiceRefObjectSequenceNumber,
      ServiceOrdItemRefObj.ServiceReferenceObjectType,
      ServiceOrdItemRefObj.ServiceReferenceObjectUUID,
      ServiceOrdItemRefObj.ProductID,
      ServiceOrdItemRefObj.SrvcRefObjIsMainObject,
      ServiceOrdItemRefObj.ProductUUID,
      ServiceOrdItemRefObj.SerialNumber,
      ServiceOrdItemRefObj.Equipment,
      ServiceOrdItemRefObj.FunctionalLocation,

      //      /* Predecessor fields */

      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'ServiceDocumentRelationUUID_2'
      ServiceDocumentPredecessor.ServiceDocumentRelationUUID,
      ServiceDocumentPredecessor.ServiceDocumentRelationUUID    as ServiceDocumentRelationUUID_2,
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'ServiceDocRltnSequenceNumber_2'
      ServiceDocumentPredecessor.ServiceDocRltnSequenceNumber,
      ServiceDocumentPredecessor.ServiceDocRltnSequenceNumber   as ServiceDocRltnSequenceNumber_2,
      ServiceDocumentPredecessor.ServiceDocumentPredecessorUUID as SrvcOrdPredecessorUUID,
      ServiceDocumentPredecessor.ServiceDocPrdcssrBusObjType    as SrvcOrdPrdcssrBusObjType,
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'SrvcDocItmRelationUUID_2'
      ServiceDocumentItemPrdcssr.ServiceDocumentItmRelationUUID,
      ServiceDocumentItemPrdcssr.ServiceDocumentItmRelationUUID as SrvcDocItmRelationUUID_2,
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'ServiceDocItmRltnSqncNumber_2'
      ServiceDocumentItemPrdcssr.ServiceDocItmRltnSqncNumber,
      ServiceDocumentItemPrdcssr.ServiceDocItmRltnSqncNumber    as ServiceDocItmRltnSqncNumber_2,
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'SrvcOrdItmPrdcssrCharUUID_2'
      ServiceDocumentItemPrdcssr.ServiceDocItmPrdcssrCharUUID   as SrvcOrdItmPrdcssrCharUUID,
      @API.element.releaseState: #DEPRECATED
      @API.element.successor: 'SrvcOrdItmPrdcssrBusObjType_2'
      ServiceDocumentItemPrdcssr.ServiceDocItmPrdcssrBusObjType as SrvcOrdItmPrdcssrBusObjType,
      ServiceOrderItemPrdcssr.ServiceDocItmPrdcssrCharUUID      as SrvcOrdItmPrdcssrCharUUID_2,
      ServiceOrderItemPrdcssr.ServiceDocItmPrdcssrBusObjType    as SrvcOrdItmPrdcssrBusObjType_2,


      /*  Associations  */
      /* Keys */
      ServiceOrderItem._ServiceObjType,
      ServiceOrderItem._ServiceDocument,

      /* Item - Product */
      ServiceOrderItem._BaseUnit_2,
      ServiceOrderItem._SalesUnit,

      /* Pricing Parameter Set */
      ServiceOrderItem._TransactionCurrency

}
where
  ServiceOrderItem.ServiceObjectType = 'BUS2000117'
```
